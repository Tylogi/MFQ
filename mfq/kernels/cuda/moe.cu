// GPU-resident MoE routing and legacy symmetric NINT8-0 kernels.
//
// The execution layout follows llama.cpp's CUDA mul_mat_id path:
//   * route ids are token-major [tokens, routes]
//   * large batches are compacted by expert into ids_dst/expert_bounds
//   * results are scattered back to token-major [tokens, routes, out]
//
// MFQ can avoid duplicating the activation during compaction. Shared expert
// inputs use [tokens, K], while routed/down-projection inputs use
// [tokens, routes, K]. ids_dst identifies either source row directly.
// Canonical NINT routed projection reuses nint_matmul_kernel from
// nint_matmul.cu; q/k metadata does not create an MFE-specific compute kernel.

#include <cuda_fp16.h>
#include "mfq_tensor_backend.h"
#include <cuda_runtime.h>
#include <mma.h>

#include <algorithm>
#include <cfloat>
#include <climits>
#include <cmath>
#include <cstdlib>
#include <cstdint>
#include <vector>

#include "moe_cache_transfer.h"
#include "glu.cuh"


void launch_nint_matmul_routed_cuda(
    mfq_tensor_backend::Tensor bitstream,
    mfq_tensor_backend::Tensor row_q_bits,
    mfq_tensor_backend::Tensor row_q_bit_offsets,
    mfq_tensor_backend::Tensor subgroup_scale,
    mfq_tensor_backend::Tensor subgroup_minimum,
    mfq_tensor_backend::Tensor neuron_scale,
    mfq_tensor_backend::Tensor neuron_minimum,
    mfq_tensor_backend::Tensor quantized_input,
    mfq_tensor_backend::Tensor input_scale,
    mfq_tensor_backend::Tensor input,
    mfq_tensor_backend::Tensor route_ids,
    mfq_tensor_backend::Tensor expert_local,
    bool route_map_ready,
    mfq_tensor_backend::Tensor ids_dst,
    mfq_tensor_backend::Tensor expert_bounds,
    mfq_tensor_backend::Tensor tile_bounds,
    mfq_tensor_backend::Tensor tile_experts,
    mfq_tensor_backend::Tensor output,
    int tokens,
    int routes,
    int experts,
    int output_rows,
    int groups,
    int padded_width,
    int input_width,
    int group_size,
    int q_expert_stride,
    int route_tile_m,
    int pool_phase,
    bool masked_experts,
    int epilogue_mode,
    bool routed_input,
    cudaStream_t stream);

namespace {

constexpr int kWarpSize = 32;
constexpr int kRowsPerBlock = 4;
constexpr int kRouteTile = 8;

__global__ void moe_cache_scatter_kernel(
        const std::uint8_t * staging,
        std::int64_t descriptor_offset,
        int transfer_count) {
    const int transfer = static_cast<int>(blockIdx.x);
    if (transfer >= transfer_count) return;
    const auto * descriptors =
        reinterpret_cast<const mfq::MoeCacheScatterDescriptor *>(
            staging + descriptor_offset);
    const auto item = descriptors[transfer];
    auto * destination = reinterpret_cast<std::uint8_t *>(
        static_cast<std::uintptr_t>(item.destination));
    const auto * source = staging + item.source_offset;
    const std::uint64_t nbytes = item.nbytes;
    if ((((reinterpret_cast<std::uintptr_t>(destination) |
            reinterpret_cast<std::uintptr_t>(source) |
            static_cast<std::uintptr_t>(nbytes)) & 15u) == 0u)) {
        auto * output = reinterpret_cast<uint4 *>(destination);
        const auto * input = reinterpret_cast<const uint4 *>(source);
        const std::uint64_t count = nbytes / sizeof(uint4);
        for (std::uint64_t index = threadIdx.x;
             index < count;
             index += blockDim.x) {
            output[index] = input[index];
        }
        return;
    }
    for (std::uint64_t index = threadIdx.x;
         index < nbytes;
         index += blockDim.x) {
        destination[index] = source[index];
    }
}

__global__ void moe_cache_mapped_gather_kernel(
        const mfq::MoeCacheMappedCopyDescriptor * descriptors,
        int transfer_count) {
    const int transfer = static_cast<int>(blockIdx.y);
    if (transfer >= transfer_count) return;
    const auto item = descriptors[transfer];
    auto * destination = reinterpret_cast<std::uint8_t *>(
        static_cast<std::uintptr_t>(item.destination));
    const auto * source = reinterpret_cast<const std::uint8_t *>(
        static_cast<std::uintptr_t>(item.source));
    const std::uint64_t nbytes = item.nbytes;
    const std::uint64_t worker =
        static_cast<std::uint64_t>(blockIdx.x) * blockDim.x + threadIdx.x;
    const std::uint64_t workers =
        static_cast<std::uint64_t>(gridDim.x) * blockDim.x;
    if ((((reinterpret_cast<std::uintptr_t>(destination) |
            reinterpret_cast<std::uintptr_t>(source) |
            static_cast<std::uintptr_t>(nbytes)) & 15u) == 0u)) {
        auto * output = reinterpret_cast<uint4 *>(destination);
        const auto * input = reinterpret_cast<const uint4 *>(source);
        const std::uint64_t count = nbytes / sizeof(uint4);
        for (std::uint64_t index = worker;
             index < count;
             index += workers) {
            output[index] = input[index];
        }
        return;
    }
    for (std::uint64_t index = worker;
         index < nbytes;
         index += workers) {
        destination[index] = source[index];
    }
}

__device__ __forceinline__ float warp_sum(float value) {
#pragma unroll
    for (int offset = 16; offset > 0; offset >>= 1) {
        value += __shfl_down_sync(0xffffffffu, value, offset);
    }
    return __shfl_sync(0xffffffffu, value, 0);
}

__device__ __forceinline__ float warp_max(float value) {
#pragma unroll
    for (int offset = 16; offset > 0; offset >>= 1) {
        value = fmaxf(value, __shfl_down_sync(0xffffffffu, value, offset));
    }
    return __shfl_sync(0xffffffffu, value, 0);
}

template <typename scalar_t>
__device__ __forceinline__ float load_float(const scalar_t * ptr, int index) {
    return static_cast<float>(ptr[index]);
}

template <>
__device__ __forceinline__ float load_float<mfq_half>(const mfq_half * ptr, int index) {
    return __half2float(*reinterpret_cast<const __half *>(ptr + index));
}

__device__ __forceinline__ float moe_sqrt_softplus(float value) {
    const float softplus = value > 20.0f ? value : log1pf(expf(value));
    return sqrtf(softplus);
}

template <typename scalar_t>
__global__ void __launch_bounds__(128) moe_topk_kernel(
        const scalar_t * __restrict__ logits,
        const float * __restrict__ bias,
        int32_t * __restrict__ ids,
        float * __restrict__ weights,
        int rows,
        int experts,
        int top_k,
        bool use_sigmoid,
        bool use_sqrt_softplus,
        bool normalize,
        bool delayed_softmax,
        float norm_floor,
        float scale) {
    const int row = blockIdx.x * blockDim.y + threadIdx.y;
    const int lane = threadIdx.x;
    if (row >= rows) {
        return;
    }

    const scalar_t * row_logits = logits + static_cast<size_t>(row) * experts;
    float softmax_max = -INFINITY;
    if (!use_sigmoid && !use_sqrt_softplus && !delayed_softmax) {
        for (int expert = lane; expert < experts; expert += kWarpSize) {
            float value = load_float(row_logits, expert);
            value = isnan(value) ? -FLT_MAX : value;
            softmax_max = fmaxf(softmax_max, value);
        }
        softmax_max = warp_max(softmax_max);
    }

    float softmax_sum = 1.0f;
    if (!use_sigmoid && !use_sqrt_softplus && !delayed_softmax) {
        softmax_sum = 0.0f;
        for (int expert = lane; expert < experts; expert += kWarpSize) {
            float value = load_float(row_logits, expert);
            value = isnan(value) ? -FLT_MAX : value;
            softmax_sum += expf(value - softmax_max);
        }
        softmax_sum = warp_sum(softmax_sum);
    }

    int selected[16];
#pragma unroll
    for (int i = 0; i < 16; ++i) {
        selected[i] = -1;
    }

    for (int rank = 0; rank < top_k; ++rank) {
        float best_score = -INFINITY;
        float best_weight = -INFINITY;
        int best_expert = INT_MAX;

        for (int expert = lane; expert < experts; expert += kWarpSize) {
            bool already_selected = false;
#pragma unroll
            for (int previous = 0; previous < 16; ++previous) {
                if (previous < rank && selected[previous] == expert) {
                    already_selected = true;
                }
            }
            if (already_selected) {
                continue;
            }

            float raw = load_float(row_logits, expert);
            raw = isnan(raw) ? -FLT_MAX : raw;
            float weight;
            if (delayed_softmax) {
                weight = raw;
            } else if (use_sigmoid) {
                weight = 1.0f / (1.0f + expf(-raw));
            } else if (use_sqrt_softplus) {
                weight = moe_sqrt_softplus(raw);
            } else {
                weight = expf(raw - softmax_max) / softmax_sum;
            }
            const float score = weight + (bias == nullptr ? 0.0f : bias[expert]);
            if (score > best_score || (score == best_score && expert < best_expert)) {
                best_score = score;
                best_weight = weight;
                best_expert = expert;
            }
        }

#pragma unroll
        for (int offset = 16; offset > 0; offset >>= 1) {
            const float other_score = __shfl_down_sync(0xffffffffu, best_score, offset);
            const float other_weight = __shfl_down_sync(0xffffffffu, best_weight, offset);
            const int other_expert = __shfl_down_sync(0xffffffffu, best_expert, offset);
            if (other_score > best_score ||
                    (other_score == best_score && other_expert < best_expert)) {
                best_score = other_score;
                best_weight = other_weight;
                best_expert = other_expert;
            }
        }
        best_weight = __shfl_sync(0xffffffffu, best_weight, 0);
        best_expert = __shfl_sync(0xffffffffu, best_expert, 0);
        selected[rank] = best_expert;
        if (lane == 0) {
            ids[static_cast<size_t>(row) * top_k + rank] = best_expert;
            weights[static_cast<size_t>(row) * top_k + rank] = best_weight;
        }
    }

    __syncwarp();
    float value = lane < top_k
        ? weights[static_cast<size_t>(row) * top_k + lane]
        : 0.0f;
    if (delayed_softmax) {
        float selected_max = lane < top_k ? value : -INFINITY;
#pragma unroll
        for (int offset = 16; offset > 0; offset >>= 1) {
            selected_max = fmaxf(
                selected_max,
                __shfl_down_sync(0xffffffffu, selected_max, offset));
        }
        selected_max = __shfl_sync(0xffffffffu, selected_max, 0);
        if (lane < top_k) {
            value = expf(value - selected_max);
        } else {
            value = 0.0f;
        }
        const float denom = warp_sum(value);
        if (lane < top_k) {
            value /= denom;
        }
    } else if (normalize) {
        float denom = warp_sum(value);
        denom = fmaxf(denom, norm_floor);
        if (lane < top_k) {
            value /= denom;
        }
    }
    if (lane < top_k) {
        weights[static_cast<size_t>(row) * top_k + lane] = value * scale;
    }
}

__device__ __forceinline__ bool moe_score_before(
        float lhs_score, int lhs_expert, float rhs_score, int rhs_expert) {
    return lhs_score > rhs_score ||
        (lhs_score == rhs_score && lhs_expert < rhs_expert);
}

__global__ void __launch_bounds__(32) moe_topk_1x128_8_kernel(
        const float * __restrict__ logits,
        int32_t * __restrict__ ids,
        float * __restrict__ weights) {
    const int lane = threadIdx.x;
    float values[4];
#pragma unroll
    for (int item = 0; item < 4; ++item) {
        float value = logits[lane + item * 32];
        values[item] = isnan(value) ? -FLT_MAX : value;
    }

    float selected = 0.0f;
#pragma unroll
    for (int rank = 0; rank < 8; ++rank) {
        float best = values[0];
        int best_expert = lane;
#pragma unroll
        for (int item = 1; item < 4; ++item) {
            const int expert = lane + item * 32;
            if (moe_score_before(values[item], expert, best, best_expert)) {
                best = values[item];
                best_expert = expert;
            }
        }
#pragma unroll
        for (int mask = 16; mask > 0; mask >>= 1) {
            const float other = __shfl_xor_sync(0xffffffffu, best, mask);
            const int other_expert = __shfl_xor_sync(0xffffffffu, best_expert, mask);
            if (moe_score_before(other, other_expert, best, best_expert)) {
                best = other;
                best_expert = other_expert;
            }
        }
        if (lane == (best_expert & 31)) {
            values[best_expert >> 5] = -INFINITY;
        }
        if (lane == rank) {
            selected = best;
            ids[rank] = best_expert;
        }
    }

    float selected_max = lane < 8 ? selected : -INFINITY;
#pragma unroll
    for (int offset = 16; offset > 0; offset >>= 1) {
        selected_max = fmaxf(
            selected_max,
            __shfl_down_sync(0xffffffffu, selected_max, offset));
    }
    selected_max = __shfl_sync(0xffffffffu, selected_max, 0);
    const float value = lane < 8 ? expf(selected - selected_max) : 0.0f;
    const float denom = warp_sum(value);
    if (lane < 8) weights[lane] = value / denom;
}

__global__ void __launch_bounds__(256) moe_topk_1x256_8_kernel(
        const float * __restrict__ logits,
        int32_t * __restrict__ ids,
        float * __restrict__ weights) {
    __shared__ float scores[256];
    __shared__ int experts[256];
    const int tid = threadIdx.x;
    float score = logits[tid];
    score = isnan(score) ? -FLT_MAX : score;
    scores[tid] = score;
    experts[tid] = tid;
    __syncthreads();

#pragma unroll
    for (int width = 2; width <= 256; width <<= 1) {
#pragma unroll
        for (int stride = width >> 1; stride > 0; stride >>= 1) {
            const int other = tid ^ stride;
            if (other > tid) {
                const float lhs_score = scores[tid];
                const int lhs_expert = experts[tid];
                const float rhs_score = scores[other];
                const int rhs_expert = experts[other];
                const bool descending = (tid & width) == 0;
                const bool lhs_before = moe_score_before(
                    lhs_score, lhs_expert, rhs_score, rhs_expert);
                const bool do_swap = descending ? !lhs_before : lhs_before;
                if (do_swap) {
                    scores[tid] = rhs_score;
                    experts[tid] = rhs_expert;
                    scores[other] = lhs_score;
                    experts[other] = lhs_expert;
                }
            }
            __syncthreads();
        }
    }

    if (tid < 32) {
        float value = tid < 8 ? scores[tid] : 0.0f;
        float selected_max = tid < 8 ? value : -INFINITY;
#pragma unroll
        for (int offset = 16; offset > 0; offset >>= 1) {
            selected_max = fmaxf(
                selected_max,
                __shfl_down_sync(0xffffffffu, selected_max, offset));
        }
        selected_max = __shfl_sync(0xffffffffu, selected_max, 0);
        value = tid < 8 ? expf(value - selected_max) : 0.0f;
        const float denom = warp_sum(value);
        if (tid < 8) {
            ids[tid] = experts[tid];
            weights[tid] = value / denom;
        }
    }
}

__global__ void __launch_bounds__(256) moe_topk_1x256_6_sqrtsoftplus_kernel(
        const float * __restrict__ logits,
        const float * __restrict__ bias,
        int32_t * __restrict__ ids,
        float * __restrict__ weights,
        float norm_floor,
        float scale) {
    __shared__ float scores[256];
    __shared__ float route_weights[256];
    __shared__ int experts[256];
    const int tid = threadIdx.x;
    float raw = logits[tid];
    raw = isnan(raw) ? -FLT_MAX : raw;
    const float weight = moe_sqrt_softplus(raw);
    route_weights[tid] = weight;
    scores[tid] = weight + (bias == nullptr ? 0.0f : bias[tid]);
    experts[tid] = tid;
    __syncthreads();

#pragma unroll
    for (int width = 2; width <= 256; width <<= 1) {
#pragma unroll
        for (int stride = width >> 1; stride > 0; stride >>= 1) {
            const int other = tid ^ stride;
            if (other > tid) {
                const float lhs_score = scores[tid];
                const int lhs_expert = experts[tid];
                const float rhs_score = scores[other];
                const int rhs_expert = experts[other];
                const bool descending = (tid & width) == 0;
                const bool lhs_before = moe_score_before(
                    lhs_score, lhs_expert, rhs_score, rhs_expert);
                const bool do_swap = descending ? !lhs_before : lhs_before;
                if (do_swap) {
                    scores[tid] = rhs_score;
                    experts[tid] = rhs_expert;
                    scores[other] = lhs_score;
                    experts[other] = lhs_expert;
                }
            }
            __syncthreads();
        }
    }

    if (tid < 32) {
        const int expert = tid < 6 ? experts[tid] : 0;
        float value = tid < 6 ? route_weights[expert] : 0.0f;
        float denom = warp_sum(value);
        denom = fmaxf(denom, norm_floor);
        if (tid < 6) {
            ids[tid] = expert;
            weights[tid] = value / denom * scale;
        }
    }
}

template <typename scalar_t>
__global__ void __launch_bounds__(128) moe_sqrtsoftplus_weights_kernel(
        const scalar_t * __restrict__ logits,
        const int32_t * __restrict__ ids,
        float * __restrict__ weights,
        int rows,
        int experts,
        int top_k,
        float norm_floor,
        float scale) {
    const int row = blockIdx.x * blockDim.y + threadIdx.y;
    const int lane = threadIdx.x;
    if (row >= rows) {
        return;
    }

    float value = 0.0f;
    if (lane < top_k) {
        const int expert = ids[static_cast<size_t>(row) * top_k + lane];
        if (static_cast<unsigned int>(expert) < static_cast<unsigned int>(experts)) {
            float raw = load_float(logits + static_cast<size_t>(row) * experts, expert);
            raw = isnan(raw) ? -FLT_MAX : raw;
            value = moe_sqrt_softplus(raw);
        }
    }
    float denom = warp_sum(value);
    denom = fmaxf(denom, norm_floor);
    if (lane < top_k) {
        weights[static_cast<size_t>(row) * top_k + lane] = value / denom * scale;
    }
}

__global__ void count_experts_kernel(
        const int32_t * __restrict__ ids,
        int32_t * __restrict__ counts,
        int pairs,
        int experts) {
    for (int pair = blockIdx.x * blockDim.x + threadIdx.x;
            pair < pairs;
            pair += blockDim.x * gridDim.x) {
        const int expert = ids[pair];
        if (static_cast<unsigned int>(expert) < static_cast<unsigned int>(experts)) {
            atomicAdd(counts + expert, 1);
        }
    }
}

__global__ void scan_expert_counts_kernel(
        const int32_t * __restrict__ counts,
        int32_t * __restrict__ cursors,
        int32_t * __restrict__ expert_bounds,
        int32_t * __restrict__ tile_bounds,
        int32_t * __restrict__ secondary_tile_bounds,
        int32_t * __restrict__ tertiary_tile_bounds,
        int experts,
        int tile_m,
        int secondary_tile_m,
        int tertiary_tile_m) {
    if (blockIdx.x != 0 || threadIdx.x != 0) {
        return;
    }
    int pair_offset = 0;
    int tile_offset = 0;
    int secondary_tile_offset = 0;
    int tertiary_tile_offset = 0;
    expert_bounds[0] = 0;
    tile_bounds[0] = 0;
    if (secondary_tile_bounds != nullptr) {
        secondary_tile_bounds[0] = 0;
    }
    if (tertiary_tile_bounds != nullptr) {
        tertiary_tile_bounds[0] = 0;
    }
    for (int expert = 0; expert < experts; ++expert) {
        const int count = counts[expert];
        const int tiles = (count + tile_m - 1) / tile_m;
        cursors[expert] = 0;
        pair_offset += count;
        tile_offset += tiles;
        expert_bounds[expert + 1] = pair_offset;
        tile_bounds[expert + 1] = tile_offset;
        if (secondary_tile_bounds != nullptr) {
            secondary_tile_offset +=
                (count + secondary_tile_m - 1) / secondary_tile_m;
            secondary_tile_bounds[expert + 1] = secondary_tile_offset;
        }
        if (tertiary_tile_bounds != nullptr) {
            tertiary_tile_offset +=
                (count + tertiary_tile_m - 1) / tertiary_tile_m;
            tertiary_tile_bounds[expert + 1] = tertiary_tile_offset;
        }
    }
}

__global__ void fill_tile_experts_kernel(
        const int32_t * __restrict__ tile_bounds,
        int32_t * __restrict__ tile_experts,
        const int32_t * __restrict__ secondary_tile_bounds,
        int32_t * __restrict__ secondary_tile_experts,
        const int32_t * __restrict__ tertiary_tile_bounds,
        int32_t * __restrict__ tertiary_tile_experts,
        int experts) {
    for (int expert = blockIdx.x * blockDim.x + threadIdx.x;
            expert < experts;
            expert += blockDim.x * gridDim.x) {
        for (int tile = tile_bounds[expert]; tile < tile_bounds[expert + 1]; ++tile) {
            tile_experts[tile] = expert;
        }
        if (secondary_tile_bounds != nullptr) {
            for (int tile = secondary_tile_bounds[expert];
                    tile < secondary_tile_bounds[expert + 1]; ++tile) {
                secondary_tile_experts[tile] = expert;
            }
        }
        if (tertiary_tile_bounds != nullptr) {
            for (int tile = tertiary_tile_bounds[expert];
                    tile < tertiary_tile_bounds[expert + 1]; ++tile) {
                tertiary_tile_experts[tile] = expert;
            }
        }
    }
}

__global__ void scatter_routes_kernel(
        const int32_t * __restrict__ ids,
        const int32_t * __restrict__ expert_bounds,
        int32_t * __restrict__ cursors,
        int32_t * __restrict__ ids_dst,
        int pairs,
        int experts) {
    for (int pair = blockIdx.x * blockDim.x + threadIdx.x;
            pair < pairs;
            pair += blockDim.x * gridDim.x) {
        const int expert = ids[pair];
        if (static_cast<unsigned int>(expert) >= static_cast<unsigned int>(experts)) {
            continue;
        }
        const int compact = expert_bounds[expert] + atomicAdd(cursors + expert, 1);
        ids_dst[compact] = pair;
    }
}

__global__ void __launch_bounds__(64) quantize_moe_input_kernel(
        const __half * __restrict__ x,
        int8_t * __restrict__ qx,
        float * __restrict__ xscale,
        int rows,
        int k_real,
        int k_pad,
        int gs) {
    const int row = blockIdx.x;
    const int group = blockIdx.y;
    const int tid = threadIdx.x;
    const int base = group * gs;
    const bool real = tid < gs && base + tid < k_real;
    const float value = real
        ? __half2float(x[static_cast<size_t>(row) * k_real + base + tid])
        : 0.0f;

    float max_value = fabsf(value);
#pragma unroll
    for (int offset = 16; offset > 0; offset >>= 1) {
        max_value = fmaxf(max_value, __shfl_down_sync(0xffffffffu, max_value, offset));
    }
    __shared__ float warp_maxima[2];
    if ((tid & 31) == 0) {
        warp_maxima[tid >> 5] = max_value;
    }
    __syncthreads();
    if (tid < 32) {
        const int active_warps = (blockDim.x + 31) / 32;
        max_value = tid < active_warps ? warp_maxima[tid] : 0.0f;
#pragma unroll
        for (int offset = 16; offset > 0; offset >>= 1) {
            max_value = fmaxf(max_value, __shfl_down_sync(0xffffffffu, max_value, offset));
        }
    }
    __shared__ float group_scale;
    if (tid == 0) {
        group_scale = max_value > 0.0f ? max_value / 127.0f : 1.0f;
        xscale[static_cast<size_t>(row) * gridDim.y + group] = group_scale;
    }
    __syncthreads();
    if (tid < gs) {
        int quant = 0;
        if (real) {
            quant = static_cast<int>(roundf(value / group_scale));
            quant = max(-127, min(127, quant));
        }
        qx[static_cast<size_t>(row) * k_pad + base + tid] = static_cast<int8_t>(quant);
    }
}

__device__ __forceinline__ int load_i8x4(const int8_t * values) {
    const uint8_t * bytes = reinterpret_cast<const uint8_t *>(values);
    return static_cast<int>(bytes[0]) |
        (static_cast<int>(bytes[1]) << 8) |
        (static_cast<int>(bytes[2]) << 16) |
        (static_cast<int>(bytes[3]) << 24);
}

template <int ITEMS_PER_BLOCK>
__global__ void nint8_zero_moe_mmvq_kernel(
        const uint8_t * __restrict__ q,
        const __half * __restrict__ scale,
        const int8_t * __restrict__ qx,
        const float * __restrict__ xscale,
        const int32_t * __restrict__ ids,
        const int32_t * __restrict__ expert_local,
        __half * __restrict__ out,
        int tokens,
        int routes,
        int experts,
        int out_per_expert,
        int groups,
        int k_pad,
        bool routed_input) {
    constexpr int gs = 32;
    constexpr int rows_per_warp = 2;
    constexpr int chunks = gs / 4;
    constexpr int groups_per_warp = kWarpSize / chunks;
    const int token =
        static_cast<int>(blockIdx.z) * ITEMS_PER_BLOCK +
        static_cast<int>(threadIdx.y);
    const int route = static_cast<int>(blockIdx.y);
    const int lane = threadIdx.x;
    const int row0 = static_cast<int>(blockIdx.x) * rows_per_warp;
    if (token >= tokens || route >= routes) {
        return;
    }
    const int pair = token * routes + route;
    const int expert = ids[pair];
    if (static_cast<unsigned int>(expert) >=
        static_cast<unsigned int>(experts)) {
        return;
    }
    const int local_expert = expert_local[expert];
    if (local_expert < 0) {
        return;
    }
    const int source_row = routed_input ? pair : token;

    float acc[rows_per_warp] = {0.0f, 0.0f};
    const int relative_group = lane / chunks;
    const int chunk = lane - relative_group * chunks;
    const int group_offset = chunk * 4;
    const bool active_lane = relative_group < groups_per_warp;

    for (int group_base = 0; group_base < groups;
         group_base += groups_per_warp) {
        const int group = group_base + relative_group;
        if (!active_lane || group >= groups) {
            continue;
        }
        const int k = group * gs + group_offset;
        const int packed_x = load_i8x4(
            qx + static_cast<size_t>(source_row) * k_pad + k);
        const float activation_scale =
            xscale[static_cast<size_t>(source_row) * groups + group];

#pragma unroll
        for (int r = 0; r < rows_per_warp; ++r) {
            const int local_row = row0 + r;
            if (local_row >= out_per_expert) {
                continue;
            }
            const int weight_row =
                local_expert * out_per_expert + local_row;
            const size_t meta =
                static_cast<size_t>(weight_row) * groups + group;
            const int packed_weight = load_i8x4(
                reinterpret_cast<const int8_t *>(
                    q + meta * gs + group_offset));
            const int dot = __dp4a(packed_weight, packed_x, 0);
            acc[r] += activation_scale *
                __half2float(scale[meta]) * static_cast<float>(dot);
        }
    }

#pragma unroll
    for (int r = 0; r < rows_per_warp; ++r) {
#pragma unroll
        for (int offset = 16; offset > 0; offset >>= 1) {
            acc[r] += __shfl_xor_sync(0xffffffffu, acc[r], offset);
        }
    }
    if (lane == 0) {
#pragma unroll
        for (int r = 0; r < rows_per_warp; ++r) {
            const int local_row = row0 + r;
            if (local_row < out_per_expert) {
                out[static_cast<size_t>(pair) * out_per_expert + local_row] =
                    __float2half(acc[r]);
            }
        }
    }
}

__device__ __forceinline__ int find_expert_tile(
        const int32_t * tile_bounds,
        int experts,
        int tile) {
    int low = 0;
    int high = experts;
    while (low < high) {
        const int middle = (low + high) >> 1;
        if (tile_bounds[middle + 1] <= tile) {
            low = middle + 1;
        } else {
            high = middle;
        }
    }
    return low;
}

template <int TILE_M>
__device__ __forceinline__ void nint8_zero_moe_grouped_tile_profile(
        const uint8_t * __restrict__ q,
        const __half * __restrict__ scale,
        const int8_t * __restrict__ qx,
        const float * __restrict__ xscale,
        const int32_t * __restrict__ ids_dst,
        __half * __restrict__ out,
        int lane,
        int row,
        int first,
        int last,
        int local_expert,
        int routes,
        int out_per_expert,
        int groups,
        bool routed_input) {
    constexpr int gs = 32;
    constexpr int chunks = gs / 4;
    constexpr int groups_per_warp = kWarpSize / chunks;
    float acc[TILE_M];
#pragma unroll
    for (int item = 0; item < TILE_M; ++item) {
        acc[item] = 0.0f;
    }
    const int relative_group = lane / chunks;
    const int chunk = lane - relative_group * chunks;
    const int group_offset = chunk * 4;
    const bool active_lane = relative_group < groups_per_warp;
    const int weight_row = local_expert * out_per_expert + row;
    const int k_pad = groups * gs;

    for (int group_base = 0; group_base < groups;
         group_base += groups_per_warp) {
        const int group = group_base + relative_group;
        if (!active_lane || group >= groups) {
            continue;
        }
        const size_t meta =
            static_cast<size_t>(weight_row) * groups + group;
        const int packed_weight = load_i8x4(
            reinterpret_cast<const int8_t *>(
                q + meta * gs + group_offset));
        const float weight_scale = __half2float(scale[meta]);
        const int k = group * gs + group_offset;

#pragma unroll
        for (int item = 0; item < TILE_M; ++item) {
            const int compact = first + item;
            if (compact >= last) {
                continue;
            }
            const int pair = ids_dst[compact];
            const int source_row = routed_input ? pair : pair / routes;
            const int packed_x = load_i8x4(
                qx + static_cast<size_t>(source_row) * k_pad + k);
            const int dot = __dp4a(packed_weight, packed_x, 0);
            const float activation_scale =
                xscale[static_cast<size_t>(source_row) * groups + group];
            acc[item] +=
                activation_scale * weight_scale * static_cast<float>(dot);
        }
    }

#pragma unroll
    for (int item = 0; item < TILE_M; ++item) {
#pragma unroll
        for (int offset = 16; offset > 0; offset >>= 1) {
            acc[item] += __shfl_xor_sync(0xffffffffu, acc[item], offset);
        }
    }
    if (lane == 0) {
#pragma unroll
        for (int item = 0; item < TILE_M; ++item) {
            const int compact = first + item;
            if (compact < last) {
                const int pair = ids_dst[compact];
                out[static_cast<size_t>(pair) * out_per_expert + row] =
                    __float2half(acc[item]);
            }
        }
    }
}

template <int TILE_M>
__global__ void __launch_bounds__(128) nint8_zero_moe_grouped_tile_kernel(
        const uint8_t * __restrict__ q,
        const __half * __restrict__ scale,
        const int8_t * __restrict__ qx,
        const float * __restrict__ xscale,
        const int32_t * __restrict__ ids_dst,
        const int32_t * __restrict__ expert_bounds,
        const int32_t * __restrict__ tile_bounds,
        const int32_t * __restrict__ expert_local,
        __half * __restrict__ out,
        int routes,
        int experts,
        int out_per_expert,
        int groups,
        int max_tiles,
        bool routed_input) {
    const int row_tiles =
        (out_per_expert + kRowsPerBlock - 1) / kRowsPerBlock;
    const int linear_block = static_cast<int>(blockIdx.x);
    const int row_tile = linear_block % row_tiles;
    const int tile = linear_block / row_tiles;
    if (tile >= max_tiles || tile >= tile_bounds[experts]) {
        return;
    }
    const int expert = find_expert_tile(tile_bounds, experts, tile);
    if (expert >= experts) {
        return;
    }
    const int local_expert = expert_local[expert];
    if (local_expert < 0) {
        return;
    }
    const int local_tile = tile - tile_bounds[expert];
    const int first = expert_bounds[expert] + local_tile * TILE_M;
    const int last = min(first + TILE_M, expert_bounds[expert + 1]);
    const int row = row_tile * kRowsPerBlock + threadIdx.y;
    if (row >= out_per_expert) {
        return;
    }
    nint8_zero_moe_grouped_tile_profile<TILE_M>(
        q, scale, qx, xscale, ids_dst, out, threadIdx.x, row, first, last,
        local_expert, routes, out_per_expert, groups, routed_input);
}

template <int TILE_M>
__global__ void __launch_bounds__(128)
nint8_zero_moe_grouped_tile_persistent_kernel(
        const uint8_t * __restrict__ q,
        const __half * __restrict__ scale,
        const int8_t * __restrict__ qx,
        const float * __restrict__ xscale,
        const int32_t * __restrict__ ids_dst,
        const int32_t * __restrict__ expert_bounds,
        const int32_t * __restrict__ tile_bounds,
        const int32_t * __restrict__ tile_experts,
        const int32_t * __restrict__ expert_local,
        __half * __restrict__ out,
        int routes,
        int experts,
        int out_per_expert,
        int groups,
        bool routed_input) {
    const int row_tiles =
        (out_per_expert + kRowsPerBlock - 1) / kRowsPerBlock;
    const int total_tiles = tile_bounds[experts];
    const int64_t total_tasks =
        static_cast<int64_t>(total_tiles) * row_tiles;
    for (int64_t task = blockIdx.x; task < total_tasks;
         task += gridDim.x) {
        const int row_tile = static_cast<int>(task % row_tiles);
        const int tile = static_cast<int>(task / row_tiles);
        const int expert = tile_experts[tile];
        const int local_expert = expert_local[expert];
        if (local_expert < 0) {
            continue;
        }
        const int local_tile = tile - tile_bounds[expert];
        const int first = expert_bounds[expert] + local_tile * TILE_M;
        const int last = min(first + TILE_M, expert_bounds[expert + 1]);
        const int row = row_tile * kRowsPerBlock + threadIdx.y;
        if (row >= out_per_expert) {
            continue;
        }
        nint8_zero_moe_grouped_tile_profile<TILE_M>(
            q, scale, qx, xscale, ids_dst, out, threadIdx.x, row, first,
            last, local_expert, routes, out_per_expert, groups,
            routed_input);
    }
}

constexpr int kMoeMmaBn = 64;
constexpr int kMoeMmaMaxBkStride = 120;

template <int GROUPS_PER_CHUNK, int BM>
__device__ __forceinline__ void nint8_zero_moe_mma_profile(
        const uint8_t * __restrict__ q,
        const __half * __restrict__ scale,
        const __half * __restrict__ x,
        const int32_t * __restrict__ ids_dst,
        __half * __restrict__ out,
        __half (*W_s)[kMoeMmaMaxBkStride],
        __half (*X_s)[kMoeMmaMaxBkStride],
        float (*C_s)[16][16],
        int first,
        int last,
        int n0,
        int local_expert,
        int routes,
        int out_per_expert,
        int weight_out_stride,
        int weight_row_offset,
        int groups,
        int k_real,
        bool routed_input) {
    constexpr int GS = 32;
    constexpr int BK = GS * GROUPS_PER_CHUNK;
    constexpr int MTILES = BM / 16;
    constexpr int NFRAGS = kMoeMmaBn / 16;
    constexpr int ACCS_PER_WARP = (MTILES + 1) / 2;
    static_assert(BK % 16 == 0 && BK <= kMoeMmaMaxBkStride);
    static_assert(BM == 16 || BM == 32 || BM == 64);

    const int lane = threadIdx.x;
    const int warp = threadIdx.y;
    const int tid = warp * 32 + lane;
    const int warp_m0 = warp / NFRAGS;
    const int warp_n = warp % NFRAGS;

    using FragA = nvcuda::wmma::fragment<nvcuda::wmma::matrix_a, 16, 16, 16,
                                         __half, nvcuda::wmma::row_major>;
    using FragB = nvcuda::wmma::fragment<nvcuda::wmma::matrix_b, 16, 16, 16,
                                         __half, nvcuda::wmma::col_major>;
    using FragC = nvcuda::wmma::fragment<nvcuda::wmma::accumulator, 16, 16, 16, float>;
    FragC acc[ACCS_PER_WARP];
#pragma unroll
    for (int a = 0; a < ACCS_PER_WARP; ++a) {
        nvcuda::wmma::fill_fragment(acc[a], 0.0f);
    }

    const int chunks = (groups + GROUPS_PER_CHUNK - 1) / GROUPS_PER_CHUNK;
    for (int chunk = 0; chunk < chunks; ++chunk) {
        const int gbase = chunk * GROUPS_PER_CHUNK;
        const int kb = gbase * GS;
        const int weight_tasks = kMoeMmaBn * GROUPS_PER_CHUNK;
        for (int task = tid; task < weight_tasks; task += 256) {
            const int nn = task / GROUPS_PER_CHUNK;
            const int gl = task - nn * GROUPS_PER_CHUNK;
            const int gn = n0 + nn;
            const int group = gbase + gl;
            const bool valid = gn < out_per_expert && group < groups;
            const int8_t * qg = nullptr;
            float d = 0.0f;
            if (valid) {
                const int weight_row =
                    local_expert * weight_out_stride + weight_row_offset + gn;
                const size_t meta = static_cast<size_t>(weight_row) * groups + group;
                qg = reinterpret_cast<const int8_t *>(q + meta * GS);
                d = __half2float(scale[meta]);
            }
#pragma unroll
            for (int i = 0; i < GS; ++i) {
                const float value = valid
                    ? d * static_cast<float>(qg[i])
                    : 0.0f;
                W_s[nn][gl * GS + i] = __float2half_rn(value);
            }
        }

        constexpr int kActivationVectorWidth = 8;
        static_assert(BK % kActivationVectorWidth == 0);
        constexpr int kActivationVectorsPerRow =
            BK / kActivationVectorWidth;
        constexpr int kActivationVectors =
            BM * kActivationVectorsPerRow;
        for (int index = tid; index < kActivationVectors; index += 256) {
            const int mm = index / kActivationVectorsPerRow;
            const int vector_local =
                index - mm * kActivationVectorsPerRow;
            const int k_local = vector_local * kActivationVectorWidth;
            const int compact = first + mm;
            const int k = kb + k_local;
            __half * destination = &X_s[mm][k_local];
            int source_row = -1;
            if (compact < last) {
                const int pair = ids_dst[compact];
                source_row = routed_input ? pair : pair / routes;
            }
            if (source_row < 0 || k >= k_real) {
                *reinterpret_cast<int4 *>(destination) =
                    make_int4(0, 0, 0, 0);
            } else if ((k_real & 7) == 0 && k + 7 < k_real) {
                *reinterpret_cast<int4 *>(destination) =
                    *reinterpret_cast<const int4 *>(
                        x + static_cast<size_t>(source_row) * k_real + k);
            } else {
#pragma unroll
                for (int element = 0;
                     element < kActivationVectorWidth;
                     ++element) {
                    destination[element] = k + element < k_real
                        ? x[static_cast<size_t>(source_row) * k_real +
                            k + element]
                        : __float2half_rn(0.0f);
                }
            }
        }
        __syncthreads();

        const bool warp_active = warp_m0 < MTILES;
#pragma unroll
        for (int ks = 0; ks < BK; ks += 16) {
            if (warp_active) {
                FragB bfrag;
                nvcuda::wmma::load_matrix_sync(
                    bfrag, &W_s[warp_n * 16][ks], kMoeMmaMaxBkStride);
#pragma unroll
                for (int a = 0; a < ACCS_PER_WARP; ++a) {
                    const int mi = warp_m0 + a * 2;
                    if (mi < MTILES) {
                        FragA afrag;
                        nvcuda::wmma::load_matrix_sync(
                            afrag, &X_s[mi * 16][ks], kMoeMmaMaxBkStride);
                        nvcuda::wmma::mma_sync(acc[a], afrag, bfrag, acc[a]);
                    }
                }
            }
        }
        __syncthreads();
    }

#pragma unroll
    for (int a = 0; a < ACCS_PER_WARP; ++a) {
        const int mi = warp_m0 + a * 2;
        const bool owns = mi < MTILES;
        if (owns) {
            nvcuda::wmma::store_matrix_sync(
                &C_s[warp][0][0], acc[a], 16, nvcuda::wmma::mem_row_major);
        }
        __syncthreads();
        if (owns) {
            const int compact0 = first + mi * 16;
            const int gn0 = n0 + warp_n * 16;
#pragma unroll
            for (int element = lane; element < 16 * 16; element += 32) {
                const int r = element / 16;
                const int c = element - r * 16;
                const int compact = compact0 + r;
                const int gn = gn0 + c;
                if (compact < last && gn < out_per_expert) {
                    const int pair = ids_dst[compact];
                    out[static_cast<size_t>(pair) * out_per_expert + gn] =
                        __float2half_rn(C_s[warp][r][c]);
                }
            }
        }
        __syncthreads();
    }
}

template <int BM, bool COARSE_TILES = false>
__global__ void __launch_bounds__(256, 1) nint8_zero_moe_mma_kernel(
        const uint8_t * __restrict__ q,
        const __half * __restrict__ scale,
        const int32_t * __restrict__ expert_local,
        const __half * __restrict__ x,
        const int32_t * __restrict__ ids_dst,
        const int32_t * __restrict__ expert_bounds,
        const int32_t * __restrict__ tile_bounds,
        const int32_t * __restrict__ tile_experts,
        __half * __restrict__ out,
        int routes,
        int experts,
        int out_per_expert,
        int groups,
        int k_real,
        bool routed_input) {
    constexpr int fine_tiles_per_mma = BM / kRouteTile;
    constexpr int groups_per_chunk = 3;
    __shared__ __half W_s[kMoeMmaBn][kMoeMmaMaxBkStride];
    __shared__ __half X_s[BM][kMoeMmaMaxBkStride];
    __shared__ float C_s[8][16][16];

    const int ntiles_n = (out_per_expert + kMoeMmaBn - 1) / kMoeMmaBn;
    const int total_tiles = tile_bounds[experts];
    const int64_t total_tasks = static_cast<int64_t>(total_tiles) * ntiles_n;
    for (int64_t task = blockIdx.x; task < total_tasks; task += gridDim.x) {
        const int tile = static_cast<int>(task / ntiles_n);
        const int ntile =
            static_cast<int>(task - static_cast<int64_t>(tile) * ntiles_n);
        const int expert = tile_experts[tile];
        const int local_tile = tile - tile_bounds[expert];
        if constexpr (!COARSE_TILES) {
            if (local_tile % fine_tiles_per_mma != 0) {
                continue;
            }
        }
        const int local_expert = expert_local[expert];
        if (local_expert < 0) {
            continue;
        }
        const int first = expert_bounds[expert] + local_tile *
            (COARSE_TILES ? BM : kRouteTile);
        const int last = min(first + BM, expert_bounds[expert + 1]);
        const int n0 = ntile * kMoeMmaBn;
        nint8_zero_moe_mma_profile<groups_per_chunk, BM>(
            q, scale, x, ids_dst, out, W_s, X_s, C_s, first, last, n0,
            local_expert, routes, out_per_expert, out_per_expert, 0,
            groups, k_real, routed_input);
    }
}

template <bool ROUND_PRODUCT>
__global__ void moe_weighted_reduce_kernel(
        const __half * __restrict__ pair_output,
        const float * __restrict__ weights,
        __half * __restrict__ output,
        int tokens,
        int routes,
        int width) {
    const size_t total = static_cast<size_t>(tokens) * width;
    for (size_t index = static_cast<size_t>(blockIdx.x) * blockDim.x + threadIdx.x;
            index < total;
            index += static_cast<size_t>(blockDim.x) * gridDim.x) {
        const int token = static_cast<int>(index / width);
        const int column = static_cast<int>(index - static_cast<size_t>(token) * width);
        float value = 0.0f;
        for (int route = 0; route < routes; ++route) {
            const size_t pair = static_cast<size_t>(token) * routes + route;
            const float expert_value = __half2float(pair_output[pair * width + column]);
            if constexpr (ROUND_PRODUCT) {
                // Preserve separate FP32 tensor multiply/add rounding.
                value = __fadd_rn(value, __fmul_rn(weights[pair], expert_value));
            } else {
                value += weights[pair] * expert_value;
            }
        }
        output[index] = __float2half(value);
    }
}

template <bool GELU>
__global__ void moe_glu_split_kernel(
        const __half * __restrict__ gate_up,
        __half * __restrict__ output,
        int rows,
        int width) {
    const int64_t index = static_cast<int64_t>(blockIdx.x) * blockDim.x + threadIdx.x;
    const int64_t total = static_cast<int64_t>(rows) * width;
    if (index >= total) {
        return;
    }
    const int row = static_cast<int>(index / width);
    const int column = static_cast<int>(index - static_cast<int64_t>(row) * width);
    const int64_t base = static_cast<int64_t>(row) * (2 * width);
    const float gate = __half2float(gate_up[base + column]);
    const float up = __half2float(gate_up[base + width + column]);
    output[index] = __float2half_rn(mfq_glu<GELU>(gate, up));
}

__global__ void moe_add_shared_gate_kernel(
        const __half * __restrict__ routed,
        const __half * __restrict__ shared,
        const float * __restrict__ gate_logits,
        __half * __restrict__ output,
        int rows,
        int width) {
    const int64_t index = static_cast<int64_t>(blockIdx.x) * blockDim.x + threadIdx.x;
    const int64_t total = static_cast<int64_t>(rows) * width;
    if (index >= total) {
        return;
    }
    const int row = static_cast<int>(index / width);
    const float gate = 1.0f / (1.0f + expf(-gate_logits[row]));
    const float value = __half2float(routed[index]) + gate * __half2float(shared[index]);
    output[index] = __float2half_rn(value);
}

__global__ void moe_weighted_reduce_shared_gate_kernel(
        const __half * __restrict__ pair_output,
        const float * __restrict__ weights,
        const __half * __restrict__ shared,
        const float * __restrict__ gate_logits,
        __half * __restrict__ output,
        int tokens,
        int routes,
        int width) {
    const size_t total = static_cast<size_t>(tokens) * width;
    for (size_t index = static_cast<size_t>(blockIdx.x) * blockDim.x + threadIdx.x;
            index < total;
            index += static_cast<size_t>(blockDim.x) * gridDim.x) {
        const int token = static_cast<int>(index / width);
        const int column = static_cast<int>(index - static_cast<size_t>(token) * width);
        float routed = 0.0f;
        for (int route = 0; route < routes; ++route) {
            const size_t pair = static_cast<size_t>(token) * routes + route;
            routed += weights[pair] * __half2float(pair_output[pair * width + column]);
        }
        routed = __half2float(__float2half_rn(routed));
        const float gate = 1.0f / (1.0f + expf(-gate_logits[token]));
        output[index] = __float2half_rn(
            routed + gate * __half2float(shared[index]));
    }
}

void check_same_device(const mfq_tensor_backend::Tensor & reference, const mfq_tensor_backend::Tensor & tensor, const char * name) {
    MFQ_RUNTIME_CHECK(tensor.device() == reference.device(), name, " must be on ", reference.device());
}

void build_expert_map(
        const mfq_tensor_backend::Tensor & ids,
        int experts,
        int tile_m,
        mfq_tensor_backend::Tensor & counts,
        mfq_tensor_backend::Tensor & cursors,
        mfq_tensor_backend::Tensor & ids_dst,
        mfq_tensor_backend::Tensor & expert_bounds,
        mfq_tensor_backend::Tensor & tile_bounds,
        mfq_tensor_backend::Tensor & tile_experts,
        cudaStream_t stream,
        mfq_tensor_backend::Tensor * secondary_tile_bounds = nullptr,
        mfq_tensor_backend::Tensor * secondary_tile_experts = nullptr,
        int secondary_tile_m = 0,
        mfq_tensor_backend::Tensor * tertiary_tile_bounds = nullptr,
        mfq_tensor_backend::Tensor * tertiary_tile_experts = nullptr,
        int tertiary_tile_m = 0) {
    const int pairs = static_cast<int>(ids.numel());
    const bool has_secondary = secondary_tile_bounds != nullptr;
    const bool has_tertiary = tertiary_tile_bounds != nullptr;
    MFQ_RUNTIME_CHECK(has_secondary == (secondary_tile_experts != nullptr),
        "secondary tile bounds and experts must be provided together");
    MFQ_RUNTIME_CHECK(!has_secondary || secondary_tile_m > 0,
        "secondary_tile_m must be positive");
    MFQ_RUNTIME_CHECK(has_tertiary == (tertiary_tile_experts != nullptr),
        "tertiary tile bounds and experts must be provided together");
    MFQ_RUNTIME_CHECK(!has_tertiary || tertiary_tile_m > 0,
        "tertiary_tile_m must be positive");
    MFQ_RUNTIME_CHECK(counts.is_cuda() && counts.is_contiguous() && counts.scalar_type() == mfq_tensor_backend::kInt32 &&
        counts.numel() >= experts, "counts workspace is too small");
    MFQ_RUNTIME_CHECK(cursors.is_cuda() && cursors.is_contiguous() && cursors.scalar_type() == mfq_tensor_backend::kInt32 &&
        cursors.numel() >= experts, "cursors workspace is too small");
    MFQ_RUNTIME_CHECK(ids_dst.is_cuda() && ids_dst.is_contiguous() && ids_dst.scalar_type() == mfq_tensor_backend::kInt32 &&
        ids_dst.numel() >= pairs, "ids_dst workspace is too small");
    MFQ_RUNTIME_CHECK(expert_bounds.is_cuda() && expert_bounds.is_contiguous() &&
        expert_bounds.scalar_type() == mfq_tensor_backend::kInt32 && expert_bounds.numel() >= experts + 1,
        "expert_bounds workspace is too small");
    MFQ_RUNTIME_CHECK(tile_bounds.is_cuda() && tile_bounds.is_contiguous() &&
        tile_bounds.scalar_type() == mfq_tensor_backend::kInt32 && tile_bounds.numel() >= experts + 1,
        "tile_bounds workspace is too small");
    MFQ_RUNTIME_CHECK(tile_experts.is_cuda() && tile_experts.is_contiguous() &&
        tile_experts.scalar_type() == mfq_tensor_backend::kInt32 && tile_experts.numel() >= pairs,
        "tile_experts workspace is too small");
    if (has_secondary) {
        MFQ_RUNTIME_CHECK(secondary_tile_bounds->is_cuda() &&
            secondary_tile_bounds->is_contiguous() &&
            secondary_tile_bounds->scalar_type() == mfq_tensor_backend::kInt32 &&
            secondary_tile_bounds->numel() >= experts + 1,
            "secondary tile_bounds workspace is too small");
        MFQ_RUNTIME_CHECK(secondary_tile_experts->is_cuda() &&
            secondary_tile_experts->is_contiguous() &&
            secondary_tile_experts->scalar_type() == mfq_tensor_backend::kInt32 &&
            secondary_tile_experts->numel() >= pairs,
            "secondary tile_experts workspace is too small");
    }
    if (has_tertiary) {
        MFQ_RUNTIME_CHECK(tertiary_tile_bounds->is_cuda() &&
            tertiary_tile_bounds->is_contiguous() &&
            tertiary_tile_bounds->scalar_type() == mfq_tensor_backend::kInt32 &&
            tertiary_tile_bounds->numel() >= experts + 1,
            "tertiary tile_bounds workspace is too small");
        MFQ_RUNTIME_CHECK(tertiary_tile_experts->is_cuda() &&
            tertiary_tile_experts->is_contiguous() &&
            tertiary_tile_experts->scalar_type() == mfq_tensor_backend::kInt32 &&
            tertiary_tile_experts->numel() >= pairs,
            "tertiary tile_experts workspace is too small");
    }
    check_same_device(ids, counts, "counts");
    check_same_device(ids, cursors, "cursors");
    check_same_device(ids, ids_dst, "ids_dst");
    check_same_device(ids, expert_bounds, "expert_bounds");
    check_same_device(ids, tile_bounds, "tile_bounds");
    check_same_device(ids, tile_experts, "tile_experts");
    if (has_secondary) {
        check_same_device(ids, *secondary_tile_bounds, "secondary_tile_bounds");
        check_same_device(ids, *secondary_tile_experts, "secondary_tile_experts");
    }
    if (has_tertiary) {
        check_same_device(ids, *tertiary_tile_bounds, "tertiary_tile_bounds");
        check_same_device(ids, *tertiary_tile_experts, "tertiary_tile_experts");
    }

    int32_t * secondary_bounds_ptr = has_secondary
        ? secondary_tile_bounds->data_ptr<int32_t>() : nullptr;
    int32_t * secondary_experts_ptr = has_secondary
        ? secondary_tile_experts->data_ptr<int32_t>() : nullptr;
    int32_t * tertiary_bounds_ptr = has_tertiary
        ? tertiary_tile_bounds->data_ptr<int32_t>() : nullptr;
    int32_t * tertiary_experts_ptr = has_tertiary
        ? tertiary_tile_experts->data_ptr<int32_t>() : nullptr;

    MFQ_CUDA_CHECK(cudaMemsetAsync(counts.data_ptr<int32_t>(), 0, experts * sizeof(int32_t), stream));
    const int block = 256;
    const int grid = std::min((pairs + block - 1) / block, 65535);
    count_experts_kernel<<<grid, block, 0, stream>>>(
        ids.data_ptr<int32_t>(), counts.data_ptr<int32_t>(), pairs, experts);
    scan_expert_counts_kernel<<<1, 1, 0, stream>>>(
        counts.data_ptr<int32_t>(), cursors.data_ptr<int32_t>(),
        expert_bounds.data_ptr<int32_t>(), tile_bounds.data_ptr<int32_t>(),
        secondary_bounds_ptr, tertiary_bounds_ptr, experts, tile_m,
        secondary_tile_m, tertiary_tile_m);
    fill_tile_experts_kernel<<<(experts + 255) / 256, 256, 0, stream>>>(
        tile_bounds.data_ptr<int32_t>(), tile_experts.data_ptr<int32_t>(),
        secondary_bounds_ptr, secondary_experts_ptr,
        tertiary_bounds_ptr, tertiary_experts_ptr, experts);
    scatter_routes_kernel<<<grid, block, 0, stream>>>(
        ids.data_ptr<int32_t>(), expert_bounds.data_ptr<int32_t>(), cursors.data_ptr<int32_t>(),
        ids_dst.data_ptr<int32_t>(), pairs, experts);
    MFQ_CUDA_KERNEL_LAUNCH_CHECK();
}

void launch_nint_quantize(
        const mfq_tensor_backend::Tensor & x,
        mfq_tensor_backend::Tensor & qx,
        mfq_tensor_backend::Tensor & xscale,
        int rows,
        int k_real,
        int k_pad,
        int groups,
        int gs,
        cudaStream_t stream) {
    const int block = gs <= 32 ? 32 : 64;
    quantize_moe_input_kernel<<<dim3(rows, groups), block, 0, stream>>>(
        reinterpret_cast<const __half *>(x.data_ptr<mfq_half>()),
        qx.data_ptr<int8_t>(), xscale.data_ptr<float>(),
        rows, k_real, k_pad, gs);
}

void launch_nint8_zero_moe_mma(
        const mfq_tensor_backend::Tensor & q,
        const mfq_tensor_backend::Tensor & scale,
        const mfq_tensor_backend::Tensor & expert_local,
        const mfq_tensor_backend::Tensor & x,
        const mfq_tensor_backend::Tensor & ids_dst,
        const mfq_tensor_backend::Tensor & expert_bounds,
        const mfq_tensor_backend::Tensor & tile_bounds,
        const mfq_tensor_backend::Tensor & tile_experts,
        mfq_tensor_backend::Tensor & out,
        int tokens,
        int routes,
        int experts,
        int out_per_expert,
        int groups,
        int k_real,
        bool routed_input,
        int route_tile_m,
        cudaStream_t stream) {
    const int ntiles_n =
        (out_per_expert + kMoeMmaBn - 1) / kMoeMmaBn;
    const int pairs = tokens * routes;
    MFQ_RUNTIME_CHECK(route_tile_m == kRouteTile || route_tile_m == 16 ||
        route_tile_m == 32 || route_tile_m == 64,
        "NINT8-0 route tile must be 8, 16, 32, or 64");
    const int64_t max_tiles =
        (pairs + route_tile_m - 1) / route_tile_m + experts;
    const int64_t max_tasks = max_tiles * ntiles_n;
    static const int block_cap = [] {
        const char * value = std::getenv("MFQ_MOE_PREFILL_MMA_BLOCKS");
        return value == nullptr ? 0 : std::max(1, std::atoi(value));
    }();
    static const int forced_bm = [] {
        const char * value = std::getenv("MFQ_MOE_PREFILL_MMA_BM");
        if (value == nullptr) return 0;
        const int parsed = std::atoi(value);
        return parsed == 16 || parsed == 32 || parsed == 64 ? parsed : 0;
    }();
    const int effective_block_cap = block_cap != 0 ? block_cap :
        (pairs >= 32768 ? 12288 : 4096);
    const int blocks = static_cast<int>(std::max<int64_t>(
        1, std::min<int64_t>(effective_block_cap, max_tasks)));
    const dim3 threads(32, 8);
    const int rows_per_expert = std::max(
        1, (pairs + experts - 1) / experts);
    const int bm = route_tile_m != kRouteTile ? route_tile_m :
        (forced_bm != 0 ? forced_bm :
            (rows_per_expert <= 16 ? 16 :
             rows_per_expert <= 32 ? 32 : 64));
    const bool coarse_tiles = route_tile_m == bm;
    MFQ_RUNTIME_CHECK(route_tile_m == kRouteTile || coarse_tiles,
        "coarse NINT8-0 route tile must match the MMA row tile");
#define MFQ_Q8_ZERO_MOE_MMA(BM_VALUE, COARSE_VALUE) \
    nint8_zero_moe_mma_kernel<BM_VALUE, COARSE_VALUE><<<blocks, threads, 0, stream>>>( \
        q.data_ptr<uint8_t>(), \
        reinterpret_cast<const __half *>(scale.data_ptr<mfq_half>()), \
        expert_local.data_ptr<int32_t>(), \
        reinterpret_cast<const __half *>(x.data_ptr<mfq_half>()), \
        ids_dst.data_ptr<int32_t>(), expert_bounds.data_ptr<int32_t>(), \
        tile_bounds.data_ptr<int32_t>(), tile_experts.data_ptr<int32_t>(), \
        reinterpret_cast<__half *>(out.data_ptr<mfq_half>()), routes, experts, \
        out_per_expert, groups, k_real, routed_input)
    if (bm == 16) {
        if (coarse_tiles) MFQ_Q8_ZERO_MOE_MMA(16, true);
        else MFQ_Q8_ZERO_MOE_MMA(16, false);
    } else if (bm == 32) {
        if (coarse_tiles) MFQ_Q8_ZERO_MOE_MMA(32, true);
        else MFQ_Q8_ZERO_MOE_MMA(32, false);
    } else {
        if (coarse_tiles) MFQ_Q8_ZERO_MOE_MMA(64, true);
        else MFQ_Q8_ZERO_MOE_MMA(64, false);
    }
#undef MFQ_Q8_ZERO_MOE_MMA
}

void launch_nint8_zero_grouped_matmul(
        const mfq_tensor_backend::Tensor & q,
        const mfq_tensor_backend::Tensor & scale,
        const mfq_tensor_backend::Tensor & qx,
        const mfq_tensor_backend::Tensor & xscale,
        const mfq_tensor_backend::Tensor & ids,
        const mfq_tensor_backend::Tensor & expert_local,
        const mfq_tensor_backend::Tensor & ids_dst,
        const mfq_tensor_backend::Tensor & expert_bounds,
        const mfq_tensor_backend::Tensor & tile_bounds,
        const mfq_tensor_backend::Tensor & tile_experts,
        mfq_tensor_backend::Tensor & out,
        int tokens,
        int routes,
        int experts,
        int out_per_expert,
        int groups,
        int k_pad,
        bool routed_input,
        cudaStream_t stream) {
    static const bool disable_token_warp = [] {
        const char * value = std::getenv("MFQ_DISABLE_MOE_TOKEN_WARP");
        return value != nullptr && std::atoi(value) != 0;
    }();
    static const int token_warps = [] {
        const char * value = std::getenv("MFQ_MOE_TOKEN_WARPS");
        if (value == nullptr) return 16;
        const int parsed = std::atoi(value);
        return parsed == 8 || parsed == 16 || parsed == 32 ? parsed : 16;
    }();
    const auto launch_direct = [&](int items) {
        const int row_blocks = (out_per_expert + 1) / 2;
#define MFQ_Q8_ZERO_MOE_DIRECT(ITEMS) \
        nint8_zero_moe_mmvq_kernel<ITEMS><<< \
            dim3(row_blocks, routes, (tokens + ITEMS - 1) / ITEMS), \
            dim3(32, ITEMS), 0, stream>>>( \
                q.data_ptr<uint8_t>(), \
                reinterpret_cast<const __half *>(scale.data_ptr<mfq_half>()), \
                qx.data_ptr<int8_t>(), xscale.data_ptr<float>(), \
                ids.data_ptr<int32_t>(), expert_local.data_ptr<int32_t>(), \
                reinterpret_cast<__half *>(out.data_ptr<mfq_half>()), \
                tokens, routes, experts, out_per_expert, groups, k_pad, \
                routed_input)
        switch (items) {
            case 1: MFQ_Q8_ZERO_MOE_DIRECT(1); break;
            case 2: MFQ_Q8_ZERO_MOE_DIRECT(2); break;
            case 4: MFQ_Q8_ZERO_MOE_DIRECT(4); break;
            case 8: MFQ_Q8_ZERO_MOE_DIRECT(8); break;
            case 16: MFQ_Q8_ZERO_MOE_DIRECT(16); break;
            case 32: MFQ_Q8_ZERO_MOE_DIRECT(32); break;
        }
#undef MFQ_Q8_ZERO_MOE_DIRECT
    };
    if (!disable_token_warp && tokens > 8 && tokens <= 64) {
        launch_direct(token_warps);
        return;
    }
    if (tokens <= 8) {
        launch_direct(tokens == 1 ? 1 : tokens <= 2 ? 2 : tokens <= 4 ? 4 : 8);
        return;
    }

    const int pairs = tokens * routes;
    const int row_tiles =
        (out_per_expert + kRowsPerBlock - 1) / kRowsPerBlock;
    static const bool disable_persistent = [] {
        const char * value = std::getenv("MFQ_DISABLE_MOE_PERSISTENT");
        return value != nullptr && std::atoi(value) != 0;
    }();
    if (!disable_persistent && tokens <= 32) {
        const int64_t max_tasks =
            static_cast<int64_t>(pairs) * row_tiles;
        constexpr int persistent_blocks = 4096;
        const int blocks = static_cast<int>(
            std::min<int64_t>(max_tasks, persistent_blocks));
        nint8_zero_moe_grouped_tile_persistent_kernel<kRouteTile><<<
            blocks, dim3(32, 4), 0, stream>>>(
                q.data_ptr<uint8_t>(),
                reinterpret_cast<const __half *>(scale.data_ptr<mfq_half>()),
                qx.data_ptr<int8_t>(), xscale.data_ptr<float>(),
                ids_dst.data_ptr<int32_t>(),
                expert_bounds.data_ptr<int32_t>(),
                tile_bounds.data_ptr<int32_t>(),
                tile_experts.data_ptr<int32_t>(),
                expert_local.data_ptr<int32_t>(),
                reinterpret_cast<__half *>(out.data_ptr<mfq_half>()),
                routes, experts, out_per_expert, groups, routed_input);
        return;
    }

    const int max_tiles =
        (pairs + kRouteTile - 1) / kRouteTile + experts;
    const int64_t blocks =
        static_cast<int64_t>(row_tiles) * max_tiles;
    MFQ_RUNTIME_CHECK(
        blocks <= INT_MAX, "grouped NINT8-0 launch grid is too large");
    nint8_zero_moe_grouped_tile_kernel<kRouteTile><<<
        static_cast<int>(blocks), dim3(32, 4), 0, stream>>>(
            q.data_ptr<uint8_t>(),
            reinterpret_cast<const __half *>(scale.data_ptr<mfq_half>()),
            qx.data_ptr<int8_t>(), xscale.data_ptr<float>(),
            ids_dst.data_ptr<int32_t>(),
            expert_bounds.data_ptr<int32_t>(),
            tile_bounds.data_ptr<int32_t>(),
            expert_local.data_ptr<int32_t>(),
            reinterpret_cast<__half *>(out.data_ptr<mfq_half>()),
            routes, experts, out_per_expert, groups, max_tiles,
            routed_input);
}

} // namespace

void mfq::moe_cache_scatter_cuda(
        const std::uint8_t * staging,
        std::int64_t descriptor_offset,
        int transfer_count,
        cudaStream_t stream) {
    MFQ_RUNTIME_CHECK(staging != nullptr, "MoE cache staging pointer is null");
    MFQ_RUNTIME_CHECK(descriptor_offset >= 0,
        "MoE cache descriptor offset must be non-negative");
    MFQ_RUNTIME_CHECK(transfer_count > 0,
        "MoE cache scatter requires at least one transfer");
    moe_cache_scatter_kernel<<<transfer_count, 256, 0, stream>>>(
        staging, descriptor_offset, transfer_count);
    MFQ_CUDA_KERNEL_LAUNCH_CHECK();
}

void mfq::moe_cache_mapped_gather_cuda(
        const MoeCacheMappedCopyDescriptor * descriptors,
        int transfer_count,
        int blocks_per_transfer,
        cudaStream_t stream) {
    MFQ_RUNTIME_CHECK(descriptors != nullptr,
        "MoE cache mapped-copy descriptor pointer is null");
    MFQ_RUNTIME_CHECK(transfer_count > 0,
        "MoE cache mapped gather requires at least one transfer");
    MFQ_RUNTIME_CHECK(blocks_per_transfer >= 1 && blocks_per_transfer <= 128,
        "MoE cache mapped gather blocks must be in [1, 128]");
    const dim3 grid(
        static_cast<unsigned int>(blocks_per_transfer),
        static_cast<unsigned int>(transfer_count));
    moe_cache_mapped_gather_kernel<<<grid, 256, 0, stream>>>(
        descriptors, transfer_count);
    MFQ_CUDA_KERNEL_LAUNCH_CHECK();
}

std::vector<mfq_tensor_backend::Tensor> moe_topk_cuda(
        mfq_tensor_backend::Tensor logits,
        int64_t top_k,
        bool use_sigmoid,
        bool use_sqrt_softplus,
        bool normalize,
        bool delayed_softmax,
        MfqOptional<mfq_tensor_backend::Tensor> bias,
        double norm_floor,
        double scale) {
    MFQ_RUNTIME_CHECK(logits.is_cuda() && logits.is_contiguous() && logits.dim() == 2,
        "logits must be contiguous CUDA [tokens, experts]");
    MFQ_RUNTIME_CHECK(logits.scalar_type() == mfq_tensor_backend::kFloat32 || logits.scalar_type() == mfq_tensor_backend::kFloat16,
        "logits must be float16 or float32");
    const int rows = static_cast<int>(logits.size(0));
    const int experts = static_cast<int>(logits.size(1));
    MFQ_RUNTIME_CHECK(rows > 0 && experts > 0, "logits dimensions must be nonzero");
    MFQ_RUNTIME_CHECK(top_k >= 1 && top_k <= 16 && top_k <= experts,
        "top_k must be in [1, min(16, experts)]");
    MFQ_RUNTIME_CHECK(!(normalize && delayed_softmax),
        "selected-weight normalization and delayed softmax are mutually exclusive");
    MFQ_RUNTIME_CHECK(!(use_sigmoid && delayed_softmax),
        "sigmoid routing and delayed softmax are mutually exclusive");
    MFQ_RUNTIME_CHECK(!(use_sqrt_softplus && delayed_softmax),
        "sqrt-softplus routing and delayed softmax are mutually exclusive");
    MFQ_RUNTIME_CHECK(!(use_sigmoid && use_sqrt_softplus),
        "sigmoid and sqrt-softplus routing are mutually exclusive");
    const float * bias_ptr = nullptr;
    if (bias.has_value()) {
        auto & value = bias.value();
        MFQ_RUNTIME_CHECK(value.is_cuda() && value.is_contiguous() && value.scalar_type() == mfq_tensor_backend::kFloat32 &&
            value.numel() == experts, "bias must be contiguous CUDA float32 [experts]");
        check_same_device(logits, value, "bias");
        bias_ptr = value.data_ptr<float>();
    }

    auto ids = mfq_tensor_backend::empty({rows, top_k}, logits.options().dtype(mfq_tensor_backend::kInt32));
    auto weights = mfq_tensor_backend::empty({rows, top_k}, logits.options().dtype(mfq_tensor_backend::kFloat32));
    const cudaStream_t stream = mfq_current_cuda_stream();
    static const bool disable_topk_sort = [] {
        const char * value = std::getenv("MFQ_DISABLE_MOE_TOPK_SORT");
        return value != nullptr && std::atoi(value) != 0;
    }();
    if (!disable_topk_sort && rows == 1 && experts == 128 && top_k == 8 &&
            logits.scalar_type() == mfq_tensor_backend::kFloat32 && bias_ptr == nullptr &&
            !use_sigmoid && !normalize && delayed_softmax && scale == 1.0) {
        moe_topk_1x128_8_kernel<<<1, 32, 0, stream>>>(
            logits.data_ptr<float>(), ids.data_ptr<int32_t>(), weights.data_ptr<float>());
        MFQ_CUDA_KERNEL_LAUNCH_CHECK();
        return {ids, weights};
    }
    if (!disable_topk_sort && rows == 1 && experts == 256 && top_k == 8 &&
            logits.scalar_type() == mfq_tensor_backend::kFloat32 && bias_ptr == nullptr &&
            !use_sigmoid && !use_sqrt_softplus && !normalize && delayed_softmax && scale == 1.0) {
        moe_topk_1x256_8_kernel<<<1, 256, 0, stream>>>(
            logits.data_ptr<float>(), ids.data_ptr<int32_t>(), weights.data_ptr<float>());
        MFQ_CUDA_KERNEL_LAUNCH_CHECK();
        return {ids, weights};
    }
    if (!disable_topk_sort && rows == 1 && experts == 256 && top_k == 6 &&
            logits.scalar_type() == mfq_tensor_backend::kFloat32 &&
            !use_sigmoid && use_sqrt_softplus && normalize && !delayed_softmax) {
        moe_topk_1x256_6_sqrtsoftplus_kernel<<<1, 256, 0, stream>>>(
            logits.data_ptr<float>(), bias_ptr, ids.data_ptr<int32_t>(), weights.data_ptr<float>(),
            static_cast<float>(norm_floor), static_cast<float>(scale));
        MFQ_CUDA_KERNEL_LAUNCH_CHECK();
        return {ids, weights};
    }
    const dim3 block(32, 4);
    const int grid = (rows + 3) / 4;
    if (logits.scalar_type() == mfq_tensor_backend::kFloat16) {
        moe_topk_kernel<mfq_half><<<grid, block, 0, stream>>>(
            logits.data_ptr<mfq_half>(), bias_ptr, ids.data_ptr<int32_t>(), weights.data_ptr<float>(),
            rows, experts, static_cast<int>(top_k), use_sigmoid, use_sqrt_softplus,
            normalize, delayed_softmax,
            static_cast<float>(norm_floor), static_cast<float>(scale));
    } else {
        moe_topk_kernel<float><<<grid, block, 0, stream>>>(
            logits.data_ptr<float>(), bias_ptr, ids.data_ptr<int32_t>(), weights.data_ptr<float>(),
            rows, experts, static_cast<int>(top_k), use_sigmoid, use_sqrt_softplus,
            normalize, delayed_softmax,
            static_cast<float>(norm_floor), static_cast<float>(scale));
    }
    MFQ_CUDA_KERNEL_LAUNCH_CHECK();
    return {ids, weights};
}

mfq_tensor_backend::Tensor moe_sqrtsoftplus_weights_cuda(
        mfq_tensor_backend::Tensor logits,
        mfq_tensor_backend::Tensor ids,
        double norm_floor,
        double scale) {
    MFQ_RUNTIME_CHECK(logits.is_cuda() && logits.is_contiguous() && logits.dim() == 2,
        "logits must be contiguous CUDA [tokens, experts]");
    MFQ_RUNTIME_CHECK(logits.scalar_type() == mfq_tensor_backend::kFloat32 || logits.scalar_type() == mfq_tensor_backend::kFloat16,
        "logits must be float16 or float32");
    MFQ_RUNTIME_CHECK(ids.is_cuda() && ids.is_contiguous() && ids.dim() == 2 &&
        ids.scalar_type() == mfq_tensor_backend::kInt32, "ids must be contiguous CUDA int32 [tokens, top_k]");
    check_same_device(logits, ids, "ids");
    MFQ_RUNTIME_CHECK(ids.size(0) == logits.size(0), "ids and logits must have the same token count");
    const int rows = static_cast<int>(logits.size(0));
    const int experts = static_cast<int>(logits.size(1));
    const int top_k = static_cast<int>(ids.size(1));
    MFQ_RUNTIME_CHECK(rows > 0 && experts > 0, "logits dimensions must be nonzero");
    MFQ_RUNTIME_CHECK(top_k >= 1 && top_k <= 16 && top_k <= experts,
        "top_k must be in [1, min(16, experts)]");

    auto weights = mfq_tensor_backend::empty(ids.sizes(), logits.options().dtype(mfq_tensor_backend::kFloat32));
    const dim3 block(32, 4);
    const int grid = (rows + 3) / 4;
    const cudaStream_t stream = mfq_current_cuda_stream();
    if (logits.scalar_type() == mfq_tensor_backend::kFloat16) {
        moe_sqrtsoftplus_weights_kernel<mfq_half><<<grid, block, 0, stream>>>(
            logits.data_ptr<mfq_half>(), ids.data_ptr<int32_t>(), weights.data_ptr<float>(),
            rows, experts, top_k, static_cast<float>(norm_floor), static_cast<float>(scale));
    } else {
        moe_sqrtsoftplus_weights_kernel<float><<<grid, block, 0, stream>>>(
            logits.data_ptr<float>(), ids.data_ptr<int32_t>(), weights.data_ptr<float>(),
            rows, experts, top_k, static_cast<float>(norm_floor), static_cast<float>(scale));
    }
    MFQ_CUDA_KERNEL_LAUNCH_CHECK();
    return weights;
}

std::vector<mfq_tensor_backend::Tensor> moe_build_expert_map_cuda(
        mfq_tensor_backend::Tensor ids,
        int64_t n_experts,
        int64_t tile_m) {
    MFQ_RUNTIME_CHECK(ids.is_cuda() && ids.is_contiguous() && ids.scalar_type() == mfq_tensor_backend::kInt32 && ids.dim() == 2,
        "ids must be contiguous CUDA int32 [tokens, routes]");
    MFQ_RUNTIME_CHECK(n_experts > 0 && n_experts <= 4096, "n_experts must be in [1, 4096]");
    MFQ_RUNTIME_CHECK(tile_m > 0 && tile_m <= 1024, "tile_m must be in [1, 1024]");
    const int experts = static_cast<int>(n_experts);
    const int pairs = static_cast<int>(ids.numel());
    auto options = ids.options();
    auto counts = mfq_tensor_backend::empty({experts}, options);
    auto cursors = mfq_tensor_backend::empty({experts}, options);
    auto ids_dst = mfq_tensor_backend::empty({pairs}, options);
    auto expert_bounds = mfq_tensor_backend::empty({experts + 1}, options);
    auto tile_bounds = mfq_tensor_backend::empty({experts + 1}, options);
    auto tile_experts = mfq_tensor_backend::empty({pairs}, options);
    const cudaStream_t stream = mfq_current_cuda_stream();
    build_expert_map(ids, experts, static_cast<int>(tile_m), counts, cursors, ids_dst,
        expert_bounds, tile_bounds, tile_experts, stream);
    return {ids_dst, expert_bounds, tile_bounds, tile_experts, counts};
}

std::vector<mfq_tensor_backend::Tensor> moe_build_expert_maps_cuda(
        mfq_tensor_backend::Tensor ids,
        int64_t n_experts,
        int64_t tile_m,
        int64_t secondary_tile_m,
        int64_t tertiary_tile_m) {
    MFQ_RUNTIME_CHECK(ids.is_cuda() && ids.is_contiguous() &&
        ids.scalar_type() == mfq_tensor_backend::kInt32 && ids.dim() == 2,
        "ids must be contiguous CUDA int32 [tokens, routes]");
    MFQ_RUNTIME_CHECK(n_experts > 0 && n_experts <= 4096,
        "n_experts must be in [1, 4096]");
    MFQ_RUNTIME_CHECK(tile_m > 0 && tile_m <= 1024 &&
        secondary_tile_m > 0 && secondary_tile_m <= 1024,
        "tile sizes must be in [1, 1024]");
    MFQ_RUNTIME_CHECK(tertiary_tile_m >= 0 && tertiary_tile_m <= 1024,
        "tertiary tile size must be zero or in [1, 1024]");
    const int experts = static_cast<int>(n_experts);
    const int pairs = static_cast<int>(ids.numel());
    auto options = ids.options();
    auto counts = mfq_tensor_backend::empty({experts}, options);
    auto cursors = mfq_tensor_backend::empty({experts}, options);
    auto ids_dst = mfq_tensor_backend::empty({pairs}, options);
    auto expert_bounds = mfq_tensor_backend::empty({experts + 1}, options);
    auto tile_bounds = mfq_tensor_backend::empty({experts + 1}, options);
    auto tile_experts = mfq_tensor_backend::empty({pairs}, options);
    auto secondary_tile_bounds = mfq_tensor_backend::empty(
        {experts + 1}, options);
    auto secondary_tile_experts = mfq_tensor_backend::empty({pairs}, options);
    const bool has_tertiary = tertiary_tile_m > 0;
    mfq_tensor_backend::Tensor tertiary_tile_bounds;
    mfq_tensor_backend::Tensor tertiary_tile_experts;
    if (has_tertiary) {
        tertiary_tile_bounds = mfq_tensor_backend::empty({experts + 1}, options);
        tertiary_tile_experts = mfq_tensor_backend::empty({pairs}, options);
    }
    const cudaStream_t stream = mfq_current_cuda_stream();
    build_expert_map(ids, experts, static_cast<int>(tile_m), counts,
        cursors, ids_dst, expert_bounds, tile_bounds, tile_experts, stream,
        &secondary_tile_bounds, &secondary_tile_experts,
        static_cast<int>(secondary_tile_m),
        has_tertiary ? &tertiary_tile_bounds : nullptr,
        has_tertiary ? &tertiary_tile_experts : nullptr,
        static_cast<int>(tertiary_tile_m));
    std::vector<mfq_tensor_backend::Tensor> result{
        ids_dst, expert_bounds, tile_bounds, tile_experts, counts,
        secondary_tile_bounds, secondary_tile_experts};
    if (has_tertiary) {
        result.push_back(tertiary_tile_bounds);
        result.push_back(tertiary_tile_experts);
    }
    return result;
}

mfq_tensor_backend::Tensor mfe_nint_matmul_ws_cuda(
        mfq_tensor_backend::Tensor q_packed,
        mfq_tensor_backend::Tensor row_q_bits,
        mfq_tensor_backend::Tensor row_q_bit_offsets,
        mfq_tensor_backend::Tensor sub_scale,
        mfq_tensor_backend::Tensor sub_min,
        mfq_tensor_backend::Tensor neuron_scale,
        mfq_tensor_backend::Tensor neuron_min,
        mfq_tensor_backend::Tensor x,
        mfq_tensor_backend::Tensor ids,
        mfq_tensor_backend::Tensor expert_local,
        int64_t n_experts,
        int64_t n_local_experts,
        int64_t out_per_expert,
        int64_t gs,
        int64_t epilogue_mode,
        bool route_map_ready,
        bool input_quantized,
        mfq_tensor_backend::Tensor out,
        mfq_tensor_backend::Tensor qx,
        mfq_tensor_backend::Tensor xscale,
        mfq_tensor_backend::Tensor ids_dst,
        mfq_tensor_backend::Tensor expert_bounds,
        mfq_tensor_backend::Tensor tile_bounds,
        mfq_tensor_backend::Tensor tile_experts,
        int64_t route_tile_m,
        int64_t pool_phase) {
    MFQ_RUNTIME_CHECK(
        n_experts > 0 && n_experts <= 4096,
        "n_experts must be in [1, 4096]");
    MFQ_RUNTIME_CHECK(
        n_local_experts > 0 && n_local_experts <= INT_MAX,
        "NINT pool capacity must be in [1, INT_MAX]");
    MFQ_RUNTIME_CHECK(
        out_per_expert > 0 && out_per_expert <= INT_MAX,
        "out_per_expert must be positive");
    MFQ_RUNTIME_CHECK(
        gs >= 4 && gs <= 64,
        "MFE NINT group size must be in [4,64]");
    MFQ_RUNTIME_CHECK(
        epilogue_mode >= 0 && epilogue_mode <= 2,
        "MFE NINT epilogue mode must be none, SwiGLU, or GeGLU");
    MFQ_RUNTIME_CHECK(
        epilogue_mode == 0 || (out_per_expert % 2) == 0,
        "MFE NINT GLU projection width must be even");
    MFQ_RUNTIME_CHECK(
        route_tile_m == 8 || route_tile_m == 16 || route_tile_m == 32 ||
            route_tile_m == 64 || route_tile_m == 128,
        "MFE NINT route tile must be 8, 16, 32, 64, or 128");
    MFQ_RUNTIME_CHECK(
        pool_phase >= 0 && pool_phase <= INT_MAX,
        "MFE NINT pool phase exceeds the CUDA index range");
    const int experts = static_cast<int>(n_experts);
    const int local_experts = static_cast<int>(n_local_experts);
    const int output_width = static_cast<int>(out_per_expert);
    const int result_width = epilogue_mode == 0
        ? output_width
        : output_width / 2;
    MFQ_RUNTIME_CHECK(
        local_experts <= INT_MAX / output_width,
        "MFE NINT row count exceeds the CUDA index range");
    const int weight_rows = local_experts * output_width;
    MFQ_RUNTIME_CHECK(
        q_packed.is_cuda() && q_packed.is_contiguous() &&
        q_packed.scalar_type() == mfq_tensor_backend::kUInt8 &&
        (q_packed.dim() == 1 ||
         (q_packed.dim() == 2 && q_packed.size(0) == local_experts &&
          q_packed.size(1) >= 8 && q_packed.size(1) <= INT_MAX)),
        "MFE NINT values must be canonical contiguous CUDA uint8");
    MFQ_RUNTIME_CHECK(
        row_q_bits.is_cuda() && row_q_bits.is_contiguous() &&
        row_q_bits.scalar_type() == mfq_tensor_backend::kUInt8 &&
        row_q_bits.numel() == weight_rows,
        "MFE NINT row widths must be contiguous CUDA uint8");
    MFQ_RUNTIME_CHECK(
        row_q_bit_offsets.is_cuda() && row_q_bit_offsets.is_contiguous() &&
        row_q_bit_offsets.scalar_type() == mfq_tensor_backend::kInt64 &&
        row_q_bit_offsets.numel() == weight_rows,
        "MFE NINT row offsets must be contiguous CUDA int64");
    MFQ_RUNTIME_CHECK(
        sub_scale.is_cuda() && sub_scale.is_contiguous() &&
        sub_scale.scalar_type() == mfq_tensor_backend::kUInt8 &&
        sub_scale.dim() == 2 && sub_scale.size(0) == weight_rows,
        "MFE NINT subgroup scales must be CUDA uint8 [rows,groups]");
    MFQ_RUNTIME_CHECK(
        sub_min.is_cuda() && sub_min.is_contiguous() &&
        sub_min.scalar_type() == mfq_tensor_backend::kUInt8 &&
        sub_min.sizes() == sub_scale.sizes(),
        "MFE NINT subgroup minima shape mismatch");
    MFQ_RUNTIME_CHECK(
        sub_scale.size(1) > 0 &&
        sub_scale.size(1) <= INT_MAX / gs,
        "MFE NINT group geometry exceeds the CUDA index range");
    const int groups = static_cast<int>(sub_scale.size(1));
    MFQ_RUNTIME_CHECK(
        neuron_scale.is_cuda() && neuron_scale.is_contiguous() &&
        neuron_scale.scalar_type() == mfq_tensor_backend::kFloat32 &&
        neuron_scale.numel() == weight_rows &&
        neuron_min.is_cuda() && neuron_min.is_contiguous() &&
        neuron_min.scalar_type() == mfq_tensor_backend::kFloat32 &&
        neuron_min.numel() == weight_rows,
        "MFE NINT neuron metadata is invalid");
    MFQ_RUNTIME_CHECK(
        ids.is_cuda() && ids.is_contiguous() &&
        ids.scalar_type() == mfq_tensor_backend::kInt32 && ids.dim() == 2,
        "ids must be contiguous CUDA int32 [tokens,routes]");
    MFQ_RUNTIME_CHECK(
        expert_local.is_cuda() && expert_local.is_contiguous() &&
        expert_local.scalar_type() == mfq_tensor_backend::kInt32 &&
        expert_local.numel() == experts,
        "expert_local must be contiguous CUDA int32 [experts]");
    MFQ_RUNTIME_CHECK(
        x.is_cuda() && x.is_contiguous() &&
        x.scalar_type() == mfq_tensor_backend::kFloat16 &&
        (x.dim() == 2 || x.dim() == 3),
        "MFE NINT input must be contiguous CUDA float16");
    MFQ_RUNTIME_CHECK(
        ids.size(0) > 0 && ids.size(0) <= INT_MAX &&
        ids.size(1) > 0 && ids.size(1) <= INT_MAX &&
        ids.size(0) <= INT_MAX / ids.size(1),
        "MFE NINT route geometry exceeds the CUDA index range");
    const int tokens = static_cast<int>(ids.size(0));
    const int routes = static_cast<int>(ids.size(1));
    const bool routed_input = x.dim() == 3;
    if (routed_input) {
        MFQ_RUNTIME_CHECK(
            x.size(0) == tokens && x.size(1) == routes,
            "routed input must have [tokens,routes,K] leading dimensions");
    } else {
        MFQ_RUNTIME_CHECK(
            x.size(0) == tokens,
            "shared input must have one row per token");
    }
    const int input_rows = routed_input ? tokens * routes : tokens;
    const int k_pad = groups * static_cast<int>(gs);
    const int q_expert_stride = q_packed.dim() == 2
        ? static_cast<int>(q_packed.size(1)) : 0;
    MFQ_RUNTIME_CHECK(
        x.size(-1) >= 0 && x.size(-1) <= INT_MAX,
        "MFE NINT input width exceeds the CUDA index range");
    const int source_width = static_cast<int>(x.size(-1));
    MFQ_RUNTIME_CHECK(
        source_width > 0 && source_width <= k_pad,
        "MFE NINT input width exceeds the packed weight width");
    const int k_real = input_quantized
        ? k_pad : static_cast<int>(x.size(-1));
    MFQ_RUNTIME_CHECK(k_real <= k_pad, "input width exceeds MFE NINT width");
    MFQ_RUNTIME_CHECK(
        qx.is_cuda() && qx.is_contiguous() &&
        qx.scalar_type() == mfq_tensor_backend::kInt8 &&
        qx.dim() == 2 && qx.size(0) >= input_rows && qx.size(1) >= k_pad,
        "MFE NINT qx workspace is too small");
    MFQ_RUNTIME_CHECK(
        xscale.is_cuda() && xscale.is_contiguous() &&
        xscale.scalar_type() == mfq_tensor_backend::kFloat32 &&
        xscale.dim() == 2 && xscale.size(0) >= input_rows &&
        xscale.size(1) >= groups,
        "MFE NINT xscale workspace is too small");
    MFQ_RUNTIME_CHECK(
        out.is_cuda() && out.is_contiguous() &&
        out.scalar_type() == mfq_tensor_backend::kFloat16 &&
        out.sizes() == mfq_tensor_backend::IntArrayRef(
            {tokens, routes, result_width}),
        "MFE NINT output shape mismatch");
    check_same_device(q_packed, row_q_bits, "row_q_bits");
    check_same_device(q_packed, row_q_bit_offsets, "row_q_bit_offsets");
    check_same_device(q_packed, sub_scale, "sub_scale");
    check_same_device(q_packed, sub_min, "sub_min");
    check_same_device(q_packed, neuron_scale, "neuron_scale");
    check_same_device(q_packed, neuron_min, "neuron_min");
    check_same_device(q_packed, x, "x");
    check_same_device(q_packed, ids, "ids");
    check_same_device(q_packed, expert_local, "expert_local");
    check_same_device(q_packed, qx, "qx");
    check_same_device(q_packed, xscale, "xscale");
    check_same_device(q_packed, out, "out");
    const bool use_compact = tokens > 8 && route_map_ready;
    const bool use_tiled_prefill = use_compact && route_tile_m > 8;
    MFQ_RUNTIME_CHECK(
        route_tile_m == 8 || use_tiled_prefill,
        "coarse MFE NINT route tiles require a compact route map");
    if (use_compact) {
        MFQ_RUNTIME_CHECK(
            ids_dst.is_cuda() && ids_dst.is_contiguous() &&
            ids_dst.scalar_type() == mfq_tensor_backend::kInt32 &&
            ids_dst.numel() == ids.numel(),
            "MFE NINT compact route ids must cover every token-route pair");
        MFQ_RUNTIME_CHECK(
            expert_bounds.is_cuda() && expert_bounds.is_contiguous() &&
            expert_bounds.scalar_type() == mfq_tensor_backend::kInt32 &&
            expert_bounds.numel() >= experts + 1 &&
            tile_bounds.is_cuda() && tile_bounds.is_contiguous() &&
            tile_bounds.scalar_type() == mfq_tensor_backend::kInt32 &&
            tile_bounds.numel() >= experts + 1,
            "MFE NINT compact expert bounds are invalid");
        MFQ_RUNTIME_CHECK(
            tile_experts.is_cuda() && tile_experts.is_contiguous() &&
            tile_experts.scalar_type() == mfq_tensor_backend::kInt32 &&
            tile_experts.numel() >= ids.numel(),
            "MFE NINT compact tile map is invalid");
        check_same_device(q_packed, ids_dst, "ids_dst");
        check_same_device(q_packed, expert_bounds, "expert_bounds");
        check_same_device(q_packed, tile_bounds, "tile_bounds");
        check_same_device(q_packed, tile_experts, "tile_experts");
    }
    const cudaStream_t stream = mfq_current_cuda_stream();

    if (!input_quantized && !use_tiled_prefill) {
        launch_nint_quantize(
            x.reshape({input_rows, k_real}), qx, xscale,
            input_rows, k_real, k_pad, groups,
            static_cast<int>(gs), stream);
    }
    launch_nint_matmul_routed_cuda(
        q_packed, row_q_bits, row_q_bit_offsets, sub_scale, sub_min,
        neuron_scale, neuron_min, qx, xscale, x, ids, expert_local,
        use_compact, ids_dst, expert_bounds, tile_bounds, tile_experts, out,
        tokens, routes, experts, output_width, groups, k_pad, source_width,
        static_cast<int>(gs), q_expert_stride, static_cast<int>(route_tile_m),
        static_cast<int>(pool_phase), local_experts < experts,
        static_cast<int>(epilogue_mode), routed_input, stream);
    return out;
}

mfq_tensor_backend::Tensor nint8_zero_moe_grouped_matmul_pool_ws_cuda(
        mfq_tensor_backend::Tensor q,
        mfq_tensor_backend::Tensor scale,
        mfq_tensor_backend::Tensor x,
        mfq_tensor_backend::Tensor ids,
        mfq_tensor_backend::Tensor expert_local,
        int64_t n_experts,
        int64_t n_local_experts,
        int64_t out_per_expert,
        bool route_map_ready,
        bool input_quantized,
        bool use_f16_mma,
        mfq_tensor_backend::Tensor out,
        mfq_tensor_backend::Tensor qx,
        mfq_tensor_backend::Tensor xscale,
        mfq_tensor_backend::Tensor counts,
        mfq_tensor_backend::Tensor cursors,
        mfq_tensor_backend::Tensor ids_dst,
        mfq_tensor_backend::Tensor expert_bounds,
        mfq_tensor_backend::Tensor tile_bounds,
        mfq_tensor_backend::Tensor tile_experts,
        int64_t route_tile_m) {
    MFQ_RUNTIME_CHECK(
        n_experts > 0 && n_experts <= 4096,
        "n_experts must be in [1, 4096]");
    MFQ_RUNTIME_CHECK(
        n_local_experts > 0 && n_local_experts <= INT_MAX,
        "NINT8-0 pool capacity must be in [1, INT_MAX]");
    MFQ_RUNTIME_CHECK(
        out_per_expert > 0 && out_per_expert <= INT_MAX,
        "out_per_expert must be positive");
    const int experts = static_cast<int>(n_experts);
    const int local_experts = static_cast<int>(n_local_experts);
    const int output_width = static_cast<int>(out_per_expert);
    MFQ_RUNTIME_CHECK(local_experts <= INT_MAX / output_width,
        "NINT8-0 row count exceeds the CUDA index range");
    MFQ_RUNTIME_CHECK(
        q.is_cuda() && q.is_contiguous() &&
        q.scalar_type() == mfq_tensor_backend::kUInt8 && q.dim() == 3 &&
        q.size(0) == static_cast<int64_t>(local_experts) * output_width &&
        q.size(2) == 32,
        "NINT8-0 MoE q must be contiguous CUDA uint8 "
        "[local_experts*out,groups,32]");
    MFQ_RUNTIME_CHECK(
        scale.is_cuda() && scale.is_contiguous() &&
        scale.scalar_type() == mfq_tensor_backend::kFloat16 && scale.dim() == 2 &&
        scale.size(0) == q.size(0) && scale.size(1) == q.size(1),
        "NINT8-0 MoE scale must be contiguous CUDA f16 "
        "[local_experts*out,groups]");
    check_same_device(q, scale, "scale");

    MFQ_RUNTIME_CHECK(
        ids.is_cuda() && ids.is_contiguous() &&
        ids.scalar_type() == mfq_tensor_backend::kInt32 && ids.dim() == 2,
        "ids must be contiguous CUDA int32 [tokens, routes]");
    MFQ_RUNTIME_CHECK(
        expert_local.is_cuda() && expert_local.is_contiguous() &&
        expert_local.scalar_type() == mfq_tensor_backend::kInt32 &&
        expert_local.dim() == 1 && expert_local.numel() == experts,
        "expert_local must be contiguous CUDA int32 [n_experts]");
    MFQ_RUNTIME_CHECK(
        x.is_cuda() && x.is_contiguous() &&
        x.scalar_type() == mfq_tensor_backend::kFloat16 &&
        (x.dim() == 2 || x.dim() == 3),
        "x must be contiguous CUDA float16 [T,K] or [T,R,K]");
    check_same_device(q, x, "x");
    check_same_device(q, ids, "ids");
    check_same_device(q, expert_local, "expert_local");
    const int tokens = static_cast<int>(ids.size(0));
    const int routes = static_cast<int>(ids.size(1));
    MFQ_RUNTIME_CHECK(tokens > 0 && routes > 0, "ids dimensions must be nonzero");
    const bool routed_input = x.dim() == 3;
    if (routed_input) {
        MFQ_RUNTIME_CHECK(
            x.size(0) == tokens && x.size(1) == routes,
            "routed x must have [tokens, routes, K] leading dimensions");
    } else {
        MFQ_RUNTIME_CHECK(
            x.size(0) == tokens,
            "shared x must have one row per token");
    }
    const int input_rows = routed_input ? tokens * routes : tokens;
    const int groups = static_cast<int>(q.size(1));
    const int k_pad = groups * 32;
    const int input_width = static_cast<int>(x.size(-1));
    const int k_real =
        input_quantized ? k_pad : static_cast<int>(x.size(-1));
    MFQ_RUNTIME_CHECK(input_width <= k_pad, "x K exceeds packed NINT8-0 K");
    MFQ_RUNTIME_CHECK(
        qx.is_cuda() && qx.is_contiguous() &&
        qx.scalar_type() == mfq_tensor_backend::kInt8 && qx.dim() == 2 &&
        qx.size(0) >= input_rows && qx.size(1) >= k_pad,
        "qx workspace is too small");
    MFQ_RUNTIME_CHECK(
        xscale.is_cuda() && xscale.is_contiguous() &&
        xscale.scalar_type() == mfq_tensor_backend::kFloat32 && xscale.dim() == 2 &&
        xscale.size(0) >= input_rows && xscale.size(1) >= groups,
        "xscale workspace is too small");
    check_same_device(q, qx, "qx");
    check_same_device(q, xscale, "xscale");
    MFQ_RUNTIME_CHECK(
        out.is_cuda() && out.is_contiguous() &&
        out.scalar_type() == mfq_tensor_backend::kFloat16 && out.dim() == 3 &&
        out.size(0) == tokens && out.size(1) == routes &&
        out.size(2) == output_width,
        "out must be contiguous CUDA float16 "
        "[tokens, routes, out_per_expert]");
    check_same_device(q, out, "out");

    const cudaStream_t stream = mfq_current_cuda_stream();
    if (tokens > 8 && !route_map_ready) {
        build_expert_map(
            ids, experts, kRouteTile, counts, cursors, ids_dst,
            expert_bounds, tile_bounds, tile_experts, stream);
    }
    if (tokens > 8) {
        MFQ_RUNTIME_CHECK(
            tile_experts.is_cuda() && tile_experts.is_contiguous() &&
            tile_experts.scalar_type() == mfq_tensor_backend::kInt32 &&
            tile_experts.numel() >= tokens * routes,
            "tile_experts workspace is too small");
        check_same_device(ids, tile_experts, "tile_experts");
    }
    if (use_f16_mma) {
        MFQ_RUNTIME_CHECK(tokens > 8, "NINT8-0 grouped MMA requires more than eight tokens");
        launch_nint8_zero_moe_mma(
            q, scale, expert_local, x, ids_dst, expert_bounds, tile_bounds,
            tile_experts, out, tokens, routes, experts, output_width, groups,
            input_width, routed_input, static_cast<int>(route_tile_m), stream);
        MFQ_CUDA_KERNEL_LAUNCH_CHECK();
        return out;
    }
    if (!input_quantized) {
        launch_nint_quantize(
            x.reshape({input_rows, k_real}), qx, xscale,
            input_rows, k_real, k_pad, groups, 32, stream);
    }
    launch_nint8_zero_grouped_matmul(
        q, scale, qx, xscale, ids, expert_local, ids_dst,
        expert_bounds, tile_bounds, tile_experts, out, tokens, routes,
        experts, output_width, groups, k_pad, routed_input, stream);
    MFQ_CUDA_KERNEL_LAUNCH_CHECK();
    return out;
}

mfq_tensor_backend::Tensor moe_weighted_reduce_cuda(
        mfq_tensor_backend::Tensor pair_output, mfq_tensor_backend::Tensor weights,
        bool round_product) {
    MFQ_RUNTIME_CHECK(pair_output.is_cuda() && pair_output.is_contiguous() &&
        pair_output.scalar_type() == mfq_tensor_backend::kFloat16 && pair_output.dim() == 3,
        "pair_output must be contiguous CUDA float16 [tokens, routes, width]");
    MFQ_RUNTIME_CHECK(weights.is_cuda() && weights.is_contiguous() &&
        weights.scalar_type() == mfq_tensor_backend::kFloat32 && weights.dim() == 2,
        "weights must be contiguous CUDA float32 [tokens, routes]");
    MFQ_RUNTIME_CHECK(pair_output.size(0) == weights.size(0) && pair_output.size(1) == weights.size(1),
        "pair_output and weights leading dimensions must match");
    check_same_device(pair_output, weights, "weights");
    const int tokens = static_cast<int>(pair_output.size(0));
    const int routes = static_cast<int>(pair_output.size(1));
    const int width = static_cast<int>(pair_output.size(2));
    auto output = mfq_tensor_backend::empty({tokens, width}, pair_output.options());
    const int block = 256;
    const int64_t total = static_cast<int64_t>(tokens) * width;
    const int grid = static_cast<int>((total + block - 1) / block);
    const cudaStream_t stream = mfq_current_cuda_stream();
    if (round_product) {
        moe_weighted_reduce_kernel<true><<<grid, block, 0, stream>>>(
            reinterpret_cast<const __half *>(pair_output.data_ptr<mfq_half>()),
            weights.data_ptr<float>(), reinterpret_cast<__half *>(output.data_ptr<mfq_half>()),
            tokens, routes, width);
    } else {
        moe_weighted_reduce_kernel<false><<<grid, block, 0, stream>>>(
            reinterpret_cast<const __half *>(pair_output.data_ptr<mfq_half>()),
            weights.data_ptr<float>(), reinterpret_cast<__half *>(output.data_ptr<mfq_half>()),
            tokens, routes, width);
    }
    MFQ_CUDA_KERNEL_LAUNCH_CHECK();
    return output;
}

mfq_tensor_backend::Tensor moe_weighted_reduce_cuda(
        mfq_tensor_backend::Tensor pair_output, mfq_tensor_backend::Tensor weights) {
    return moe_weighted_reduce_cuda(pair_output, weights, false);
}

mfq_tensor_backend::Tensor moe_swiglu_split_cuda(mfq_tensor_backend::Tensor gate_up) {
    MFQ_RUNTIME_CHECK(gate_up.is_cuda() && gate_up.is_contiguous() &&
        gate_up.scalar_type() == mfq_tensor_backend::kFloat16 && gate_up.dim() == 3,
        "gate_up must be contiguous CUDA float16 [tokens, routes, 2 * width]");
    MFQ_RUNTIME_CHECK(gate_up.size(2) > 0 && gate_up.size(2) % 2 == 0,
        "gate_up width must be positive and even");
    const int tokens = static_cast<int>(gate_up.size(0));
    const int routes = static_cast<int>(gate_up.size(1));
    const int width = static_cast<int>(gate_up.size(2) / 2);
    MFQ_RUNTIME_CHECK(tokens > 0 && routes > 0, "gate_up leading dimensions must be nonzero");
    auto output = mfq_tensor_backend::empty({tokens, routes, width}, gate_up.options());
    const int rows = tokens * routes;
    const int64_t total = static_cast<int64_t>(rows) * width;
    constexpr int block = 256;
    const int grid = static_cast<int>((total + block - 1) / block);
    moe_glu_split_kernel<false><<<grid, block, 0, mfq_current_cuda_stream()>>>(
        reinterpret_cast<const __half *>(gate_up.data_ptr<mfq_half>()),
        reinterpret_cast<__half *>(output.data_ptr<mfq_half>()), rows, width);
    MFQ_CUDA_KERNEL_LAUNCH_CHECK();
    return output;
}

mfq_tensor_backend::Tensor moe_geglu_split_cuda(mfq_tensor_backend::Tensor gate_up) {
    MFQ_RUNTIME_CHECK(gate_up.is_cuda() && gate_up.is_contiguous() &&
        gate_up.scalar_type() == mfq_tensor_backend::kFloat16 && gate_up.dim() >= 2,
        "gate_up must be contiguous CUDA float16 [..., 2 * width]");
    MFQ_RUNTIME_CHECK(gate_up.size(-1) > 0 && gate_up.size(-1) % 2 == 0,
        "gate_up width must be positive and even");
    const int width = static_cast<int>(gate_up.size(-1) / 2);
    const int64_t rows64 = gate_up.numel() / (2 * width);
    MFQ_RUNTIME_CHECK(rows64 > 0 && rows64 <= INT_MAX, "gate_up row count is unsupported");
    auto shape = gate_up.sizes().vec();
    shape.back() = width;
    auto output = mfq_tensor_backend::empty(shape, gate_up.options());
    constexpr int block = 256;
    const int64_t total = rows64 * width;
    const int grid = static_cast<int>((total + block - 1) / block);
    moe_glu_split_kernel<true><<<grid, block, 0, mfq_current_cuda_stream()>>>(
        reinterpret_cast<const __half *>(gate_up.data_ptr<mfq_half>()),
        reinterpret_cast<__half *>(output.data_ptr<mfq_half>()),
        static_cast<int>(rows64), width);
    MFQ_CUDA_KERNEL_LAUNCH_CHECK();
    return output;
}

__global__ void moe_apply_expert_scale_kernel(
        float * __restrict__ weights,
        const int32_t * __restrict__ ids,
        const float * __restrict__ scales,
        int total) {
    for (int index = blockIdx.x * blockDim.x + threadIdx.x;
         index < total;
         index += blockDim.x * gridDim.x) {
        weights[index] *= scales[ids[index]];
    }
}

mfq_tensor_backend::Tensor moe_apply_expert_scale_cuda(
        mfq_tensor_backend::Tensor weights, mfq_tensor_backend::Tensor ids, mfq_tensor_backend::Tensor scales) {
    MFQ_RUNTIME_CHECK(weights.is_cuda() && weights.is_contiguous() &&
        weights.scalar_type() == mfq_tensor_backend::kFloat32 && weights.dim() == 2,
        "weights must be contiguous CUDA float32 [tokens, routes]");
    MFQ_RUNTIME_CHECK(ids.is_cuda() && ids.is_contiguous() &&
        ids.scalar_type() == mfq_tensor_backend::kInt32 && ids.sizes() == weights.sizes(),
        "ids must be contiguous CUDA int32 with the same shape as weights");
    MFQ_RUNTIME_CHECK(scales.is_cuda() && scales.is_contiguous() &&
        scales.scalar_type() == mfq_tensor_backend::kFloat32 && scales.dim() == 1,
        "scales must be contiguous CUDA float32 [experts]");
    check_same_device(weights, ids, "ids");
    check_same_device(weights, scales, "scales");
    const int64_t total64 = weights.numel();
    MFQ_RUNTIME_CHECK(total64 <= INT_MAX, "expert scale tensor is too large");
    constexpr int block = 256;
    const int grid = static_cast<int>((total64 + block - 1) / block);
    moe_apply_expert_scale_kernel<<<grid, block, 0, mfq_current_cuda_stream()>>>(
        weights.data_ptr<float>(), ids.data_ptr<int32_t>(), scales.data_ptr<float>(),
        static_cast<int>(total64));
    MFQ_CUDA_KERNEL_LAUNCH_CHECK();
    return weights;
}

mfq_tensor_backend::Tensor moe_add_shared_gate_cuda(
        mfq_tensor_backend::Tensor routed,
        mfq_tensor_backend::Tensor shared,
        mfq_tensor_backend::Tensor gate_logits) {
    MFQ_RUNTIME_CHECK(routed.is_cuda() && routed.is_contiguous() &&
        routed.scalar_type() == mfq_tensor_backend::kFloat16 && routed.dim() == 2,
        "routed must be contiguous CUDA float16 [tokens, width]");
    MFQ_RUNTIME_CHECK(shared.is_cuda() && shared.is_contiguous() &&
        shared.scalar_type() == mfq_tensor_backend::kFloat16 && shared.sizes() == routed.sizes(),
        "shared must match routed as contiguous CUDA float16");
    MFQ_RUNTIME_CHECK(gate_logits.is_cuda() && gate_logits.is_contiguous() &&
        gate_logits.scalar_type() == mfq_tensor_backend::kFloat32 && gate_logits.dim() == 2 &&
        gate_logits.size(0) == routed.size(0) && gate_logits.size(1) == 1,
        "gate_logits must be contiguous CUDA float32 [tokens, 1]");
    check_same_device(routed, shared, "shared");
    check_same_device(routed, gate_logits, "gate_logits");
    const int rows = static_cast<int>(routed.size(0));
    const int width = static_cast<int>(routed.size(1));
    auto output = mfq_tensor_backend::empty_like(routed);
    const int64_t total = static_cast<int64_t>(rows) * width;
    constexpr int block = 256;
    const int grid = static_cast<int>((total + block - 1) / block);
    moe_add_shared_gate_kernel<<<grid, block, 0, mfq_current_cuda_stream()>>>(
        reinterpret_cast<const __half *>(routed.data_ptr<mfq_half>()),
        reinterpret_cast<const __half *>(shared.data_ptr<mfq_half>()),
        gate_logits.data_ptr<float>(),
        reinterpret_cast<__half *>(output.data_ptr<mfq_half>()), rows, width);
    MFQ_CUDA_KERNEL_LAUNCH_CHECK();
    return output;
}

mfq_tensor_backend::Tensor moe_weighted_reduce_shared_gate_cuda(
        mfq_tensor_backend::Tensor pair_output,
        mfq_tensor_backend::Tensor weights,
        mfq_tensor_backend::Tensor shared,
        mfq_tensor_backend::Tensor gate_logits) {
    MFQ_RUNTIME_CHECK(pair_output.is_cuda() && pair_output.is_contiguous() &&
        pair_output.scalar_type() == mfq_tensor_backend::kFloat16 && pair_output.dim() == 3,
        "pair_output must be contiguous CUDA float16 [tokens, routes, width]");
    MFQ_RUNTIME_CHECK(weights.is_cuda() && weights.is_contiguous() &&
        weights.scalar_type() == mfq_tensor_backend::kFloat32 && weights.dim() == 2 &&
        pair_output.size(0) == weights.size(0) && pair_output.size(1) == weights.size(1),
        "weights must match pair_output as contiguous CUDA float32 [tokens, routes]");
    MFQ_RUNTIME_CHECK(shared.is_cuda() && shared.is_contiguous() &&
        shared.scalar_type() == mfq_tensor_backend::kFloat16 && shared.dim() == 2 &&
        shared.size(0) == pair_output.size(0) && shared.size(1) == pair_output.size(2),
        "shared must be contiguous CUDA float16 [tokens, width]");
    MFQ_RUNTIME_CHECK(gate_logits.is_cuda() && gate_logits.is_contiguous() &&
        gate_logits.scalar_type() == mfq_tensor_backend::kFloat32 && gate_logits.dim() == 2 &&
        gate_logits.size(0) == pair_output.size(0) && gate_logits.size(1) == 1,
        "gate_logits must be contiguous CUDA float32 [tokens, 1]");
    check_same_device(pair_output, weights, "weights");
    check_same_device(pair_output, shared, "shared");
    check_same_device(pair_output, gate_logits, "gate_logits");
    const int tokens = static_cast<int>(pair_output.size(0));
    const int routes = static_cast<int>(pair_output.size(1));
    const int width = static_cast<int>(pair_output.size(2));
    auto output = mfq_tensor_backend::empty({tokens, width}, pair_output.options());
    constexpr int block = 256;
    const int64_t total = static_cast<int64_t>(tokens) * width;
    const int grid = static_cast<int>((total + block - 1) / block);
    moe_weighted_reduce_shared_gate_kernel<<<
        grid, block, 0, mfq_current_cuda_stream()>>>(
        reinterpret_cast<const __half *>(pair_output.data_ptr<mfq_half>()),
        weights.data_ptr<float>(),
        reinterpret_cast<const __half *>(shared.data_ptr<mfq_half>()),
        gate_logits.data_ptr<float>(),
        reinterpret_cast<__half *>(output.data_ptr<mfq_half>()),
        tokens, routes, width);
    MFQ_CUDA_KERNEL_LAUNCH_CHECK();
    return output;
}
